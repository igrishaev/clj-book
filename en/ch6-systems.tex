\chapter{Systems}

\label{chapter-systems}

\index{systems}

\begin{teaser}
In this chapter, I'll tell you about systems, i.e., collections of interrelated components. We'll look at how large projects are assembled of small parts as well as how to overcome complexity and make the parts work as one.
\end{teaser}

The concept of a system is related to the configuration we just discussed. The configuration answers the question of how to \emph{get} the parameters, and the system knows how \emph{to use} them.

Systems emerged when the demand for long-running applications arose. We don't require such things from scripts and utilities as they have a short runtime and a state that does not last long. When scripts and utilities finish their work, resources get released, so there is no point in monitoring them.

Things are different for server applications: they work all the time, and therefore, are designed differently than scripts. The application consists of components that run in the background. Each component performs its specific task. At startup, the app enables enables components in the correct order and builds connections between them.

\section{More Details on Systems}

\index{state}

A component is a stateful object. It is affected by the "on" and "off" operations. As a rule, to enable a component means to open the resource, and to disable~--- means to close it.

\index{dependency graph}

Typical application components are server, database, and cache. In order not to open a connection for every database request, you need a connection pool. I should hate to create it manually and pass it into JDBC functions. There should be a component that, when enabled, opens the pool and stores it. Such a component offers consumers methods for working with a database. Internally, they use an open pool.

\index{OOP}

At first glance, the scheme resembles OOP and encapsulation. But don't jump to conclusions: components in Clojure work differently. Below we will look at how objects differ from components.

\subsection{Dependencies}

The main point of the system is component dependencies. A server, database, and cache are independent of each other. These are the base components of the system on which others, a level higher, rely. Suppose a background thread reads the database and sends emails. It will be wrong if the component opens new connections to the database and email. Instead, it takes the enabled components and works with them like with a black box.

A system starts and stops components in the correct order. If component A depends on B and C, then by the time A starts up, the other two should be enabled. On completion, the B and C components must not be shut down as long as A is running because this will disrupt its operation. The system builds a dependency graph between components. The graph is traversed so as to satisfy all participants.

It should be easy to add a new component to a system. Ideally, the system is a combination of maps and lists.  The boot code runs through them and enables the components. Extending the system means adding a new node to the tree.

Once the system is aware of the dependencies, a subset of it can be included. Let's say you need to debug an email handler that depends on a database and SMTP server. You don't need the web server and cache in this case, and starting the entire system is redundant. Advanced systems offer a feature with "run this component and its dependencies" semantics.

\subsection{Advantages}

At first glance, it seems that the system only overcomplicates things. It will require a new library, team conventions, and refactoring. However, the inconvenience pays off over time.

The system puts the project in order. As the codebase grows, it becomes important that the components of a project are in the same style. If you follow this path, the service components will go to the libraries, and only the logic will remain in the project. It's easier to start a project with a tried-and-tested base of internal components.

Systems are useful at all stages, especially when testing. Tests run a system where some components work differently. For example, an SMS sender writes messages to a file or atom. The authorization component reads the verification code from these sources. The approach does not guarantee complete reliability, but it will execute tests in isolation, without resorting to third-party services. We'll go into more detail about the problem of isolation in the chapter on tests \page{test-isolation}.

\section{Preparing for the Overview}

\index{variables!global}
\index{clojure.core!alter-var-root}

We've talked a little about systems in the chapter on mutable data \page{systems-intro}. The method uses \verb|alter-var-root| and global variables. The idea is to move the component into a module and provide the \verb|start!| and \verb|stop!| functions, which shift the module state. Starting the system comes down to calling them in the correct order.

That is an amateur solution because the system is not aware of the dependencies between the components. It is fragile, operates in manual mode, and each change requires verification.

Clojure offers several libraries for systems. We'll take a look at Mount, Component, and Integrant. These libraries differ in their approach to describing components and dependencies. So we will look at the problem from different angles.

I deliberately arranged the libraries in this order. Mount is the simplest of them, so let's start with it as an introduction to the topic. Component has become an industry standard. Let's pay more attention to it and therefore put it in the middle. Integrant completes our overview. We'll discuss it as an alternative to the Component library, which you will be familiar with by then.

Our system is similar to what you might come across in practice. It consists of a web server, a database, and a worker (a background task that updates records in the database). We added the worker specifically to help you learn how to work with dependencies. To better understand the system, let's draw its topology \fig{fig:chart-system}.

\begin{figure}[ht!]
  \chart{chart-sys-1}
  \caption{Diagram of a future system with dependencies}
  \label{fig:chart-system}
\end{figure}

Arrows indicate relationships between components. The expression ~\arr~B means "A depends on B." In our block diagram, all components are configuration dependent. The background worker additionally needs the database. We will work on this system until the end of the chapter.

\subsection{Database and Worker \}

We've mentioned previously that opening a connection for every request is suboptimal. In real projects, they work with the base through a pool. It is a stateful entity, so it is can be on and off as well.

\index{databases!SQLite}

Databases like SQLite and H2 store data in memory. This is convenient for a quick start, but does not reflect the realities of production, which is what we are aiming for in this book. For in-memory databases, connection pools are not used because the data is in memory, rather than in the network. We will work with the PostgreSQL relational database and the HikariCP pool.

The background process (aka worker) supplements the records in the database with information from the network. Let's say the company conducts an analysis of its website visits. When someone opens the page, the app stores the client's URL and IP address. To build reports by country and city, you need to get geodata by IP from third-party services. This operation is long, so the "in processing" flag is set for the database record and the logic is moved to the background.

\subsection{Docker}

\index{Docker}

\label{docker-db}

If you have PostgreSQL installed, create a new database and a table in it. If not, it's time to try Docker. It is a program for running applications from images. Here, an image is a package containing an app and everything needed for running. The running image is called a container.

Containers have several advantages. The application lives in an isolated environment and, therefore, is separated from the main system. Along with the safety problem, this solves the problem of \emph{cleanliness} — the container does not leave traces of working unless otherwise indicated.

Docker searches for images in an open repository where programs of different versions and bundles are published. If you specifically need PostgreSQL version 9.3, download the image with this tag. Installing this version on the system will most likely result in a conflict with an already working database.

\mnoindent
Some images can be configured with environment variables or files. At startup, a PostgreSQL image loads all \verb|*.sql| files from the \texttt{/docker-entrypoint--initdb.d} folder. If we associate the folder to the local path with migrations, we'll get a ready-to-use database. At the same time, we did not write a single line of code but only specified the settings.

\index{utilities!docker-compose}

Docker includes the \texttt{docker\--compose} utility. It runs a container from a config file. By default, the file is named \verb|docker-compose.yaml|. It is a YAML document that specifies running options and the image. We have set up the \verb|postgres| image and its options: the port, file paths, and environment variables.

\index{formats!YAML}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{yaml}
version: '2'
services:
  postgres:
    image: postgres
    volumes:
      - ./initdb.d:/docker-entrypoint-
                                initdb.d
    ports:
      - 5432:5432
    environment:
      POSTGRES_DB: book
      POSTGRES_USER: book
      POSTGRES_PASSWORD: book
  \end{yaml}
\end{english}

\else

\begin{english}
  \begin{yaml}
version: '2'
services:
  postgres:
    image: postgres
    volumes:
      - ./initdb.d:/docker-entrypoint-initdb.d
    ports:
      - 5432:5432
    environment:
      POSTGRES_DB: book
      POSTGRES_USER: book
      POSTGRES_PASSWORD: book
  \end{yaml}
\end{english}

\fi

\mnoindent
The \verb|initdb.d| folder contains SQL files for starting the database. The \verb|01.init.sql| file with the table is enough:

\index{languages!SQL}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{sql}
drop table if exists requests;
create table requests (
    id            serial primary key,
    path          text not null,
    ip            inet not null,
    is_processed  boolean not null
                  default false,
    zip           text,
    country       text,
    city          text,
    lat           float,
    lon           float
);
  \end{sql}
\end{english}

\else

\begin{english}
  \begin{sql}
drop table if exists requests;
create table requests (
    id            serial primary key,
    path          text not null,
    ip            inet not null,
    is_processed  boolean not null default false,
    zip           text,
    country       text,
    city          text,
    lat           float,
    lon           float
);
  \end{sql}
\end{english}

\fi

Running \verb|docker-compose up| brings up the PostgreSQL server on port 5432 with the \verb|book| database. This is enough for further work. We will not talk about Docker anymore, as this is a topic for a separate book. You can find all details \footurl{on the project website}{https://docker.com}[Docker][-10mm].

\section{Mount}

\index{libraries!Mount}

The \footurl{Mount}{https://github.com/tolitius/mount}[Mount][1mm] library describes an entity with two states: starting and stopping. On command, the entity gets "on" and takes the value returned by the startup code. On shutdown, the stop code will work. The entity is like a global variable that changes on command. Mount is simple, so it's perfect for beginners. We will start the practical part with it.

\subsection{First Entity}

\index{macros!defstate}

The \verb|defstate| macro defines a new entity. This macro is somewhat similar to \verb|def| because it declares a variable in the current space. The difference is that \verb|defstate| takes startup and shutdown code instead of a value.

Add the Mount dependency to the project. Let's describe the web server component using the macro. We'll put it in the \verb|server.clj| module. The \verb|app| function is a primitive application that returns the 200 status on all requests.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
;; project.clj
:dependencies [... [mount "0.1.16"]]

;; server.clj
(ns book.systems.mount.server
  (:require
   [mount.core :as mount
    :refer [defstate]]
   [ring.adapter.jetty
    :refer [run-jetty]]))

(def app (constantly
           {:status 200 :body "Hello"}))

(defstate server
  :start (run-jetty app
           {:join? false :port 8080})
  :stop (.stop server))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
;; project.clj
:dependencies [... [mount "0.1.16"]]

;; server.clj
(ns book.systems.mount.server
  (:require
   [mount.core :as mount :refer [defstate]]
   [ring.adapter.jetty :refer [run-jetty]]))

(def app (constantly {:status 200 :body "Hello"}))

(defstate server
  :start (run-jetty app {:join? false :port 8080})
  :stop (.stop server))
  \end{clojure}
\end{english}

\fi

So far, we have only declared the state but have not turned anything on. If we execute \verb|server|, we should see the following:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
#DerefableState[{:status :pending
                 :val nil}]
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
#DerefableState[{:status :pending, :val nil}]
  \end{clojure}
\end{english}

\fi

\mnoindent
Execute \texttt{(mount\-/start)} to start the component. The function runs through the components and turns them on. The \verb| (run-jetty ...)| expression under the \verb|:start| key will return the server running in the background. Once started, your browser will show a greeting at \verb|http://127.0.0.1:8080|. The \verb|server| variable will become the \verb|Server| instance from the \verb|jetty| package:

\begin{english}
  \begin{clojure}
(type server)
;; org.eclipse.jetty.server.Server
  \end{clojure}
\end{english}

To stop the system, execute \verb|(mount/stop)|. Note that in the \verb| (.stop server)| expression, the entity refers to itself. After stopping, \verb|server| will become a value that signifies termination.

\begin{english}
  \begin{clojure}
(mount/stop)
(type server)
;; mount.core.NotStartedState
  \end{clojure}
\end{english}

This is how the system is built. First, we find entities that run throughout the entire program. These are mainly network connections and background tasks. Then we declare a component with a start and stop logic.

\subsection{Relationship with Configuration}

Above, we made a mistake by explicitly setting the server parameters in the component. We have already discussed why this is a bad thing; parameters must be in configuration \page{hardcode}. Since we have a system, let's move the config into a component.

\index{config!in Mount}

The \verb|config| entity is a lightweight configuration loader. For the sake of brevity, we will skip catching errors and other details. The \verb|:start| phase reads the EDN file. Replace \texttt{edn/read\--string}| with Yummy, Aero, or your solution. The component doesn't need the \verb|stop| phase because it is stateless.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defstate config
  :start
  (-> "system.config.edn"
      slurp
      edn/read-string))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defstate config
  :start
  (-> "system.config.edn" slurp edn/read-string))
  \end{clojure}
\end{english}

\fi

The \verb|system.config.edn| file contains a map, in which the key is the component name, and the value is parameters. Let's put the server under the \verb|:jetty| key:

\begin{english}
  \begin{clojure}
{:jetty {:join? false :port 8088}}
  \end{clojure}
\end{english}

\index{dependencies!Mount}

Let's modify the server so that it depends on the config. Add the \verb|config| component to the \verb|require| list of the module:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.mount.server
 (:require
  [book.systems.mount.config
   :refer [config]]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.mount.server
 (:require
  [book.systems.mount.config :refer [config]]))
  \end{clojure}
\end{english}

\fi

\noindent
Let's rewrite the server to read the configuration:

\begin{english}
  \begin{clojure}
(defstate server
  :start
  (let [{jetty-opt :jetty} config]
    (run-jetty app jetty-opt))
  :stop (.stop server))
  \end{clojure}
\end{english}

The result is a system of two components, where one depends on the other. Make sure that after calling \verb|(mount/start)|, the server is working as expected.

\subsection{Databases}

\index{libraries!JDBC}
\index{libraries!HikariCP}

Let's prepare a database component. We'll need \footurl{JDBC}{https://github.com/clojure/java.jdbc}[JDBC][1mm] and \footurl{HikariCP}{https://github.com/tomekw/hikari-cp}[HikariCP][20mm] libraries. The former offers access to relational databases. It is a set of functions that work the same for different bases. The following statements will read and write a user to PostgreSQL, MySQL, or Oracle:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(jdbc/get-by-id db :users 42)
(jdbc/insert! db :users
  {:name "Ivan" :email "ivan@test.com"})
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(jdbc/get-by-id db :users 42)
(jdbc/insert! db :users {:name "Ivan" :email "ivan@test.com"})
  \end{clojure}
\end{english}

\fi

\noindent
For each backend, JDBC builds a request taking into account its peculiarities.

\index{JDBC!spec}

JDBC functions take \emph{the JDBC spec} as the first parameter. Usually, it is a connection map: the server address and port, the database name, the user, and the password. For each request, JDBC opens a connection, exchanges data, and closes the connection.

The spec may contain the \verb|:datasource| key with a ready data source. In this case, JDBC ignores other keys and works directly with ~\verb|:datasource|. HikariCP offers a function to build a source with a connection pool. Each time we request a connection from the source, we'll get one of the previously opened.

\index{JDBC!datasource}

The source stores the state, so let's move it into the component. Let's prepare the \verb|db.clj| module:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.mount.db
  (:require
   [mount.core :as mount
    :refer [defstate]]
   [hikari-cp.core :as cp]
   [book.systems.mount.config
    :refer [config]]))

(defstate db
  :start
  (let [{pool-opt :pool} config
        store (cp/make-datasource
                pool-opt)]
    {:datasource store})
  :stop
  (-> db
      :datasource
      cp/close-datasource))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.mount.db
  (:require
   [mount.core :as mount :refer [defstate]]
   [hikari-cp.core :as cp]
   [book.systems.mount.config :refer [config]]))

(defstate db
  :start
  (let [{pool-opt :pool} config
        store (cp/make-datasource pool-opt)]
    {:datasource store})
  :stop
  (-> db :datasource cp/close-datasource))
  \end{clojure}
\end{english}

\fi

\index{connection pool}

The start code will return a JDBC spec~--- a map with the \verb|:datasource| key, inside which there is a pool. On terminate, the \verb|close-datasource| function closes it along with all opened connections. Let's add the pool settings to the config:

\begin{english}
  \begin{clojure}
{:pool {:minimum-idle       10
        :maximum-pool-size  10
        :adapter            "postgresql"
        :username           "book"
        :password           "book"
        :database-name      "book"
        :server-name        "127.0.0.1"
        :port-number        5432}}
  \end{clojure}
\end{english}

To save space, we will only indicate the basic parameters. These are connection properties (host, port, user, password) and the pool dimension. If desired, you can configure connection timings: idle time, connection time, and others. For a complete list of options, see the project's GitHub page.

Start the system and execute a request:

\begin{english}
  \begin{clojure}
(mount/start)
(require '[clojure.java.jdbc :as jdbc])
(jdbc/query db "select 1 as number")
;; ({:number 1})
  \end{clojure}
\end{english}

\subsection{Background Task}

\label{worker}

Everything is ready for the last system component. It is a worker that runs on a separate thread. It selects raw records from the database and supplements them with fields from a third-party service.

The \verb|requests| table stores the page address and client IP. The \verb|is_processed| flag indicates whether the entry has already been processed. The \verb|city|, \verb|country|, and other fields are \verb|NULL| by default.

\index{null!in SQL}

The worker cycle consists of the following steps:

\begin{itemize}

\item
  once per interval, select an entry with the \verb|NOT is_processed| flag;

\item
  request a service that will return geodata by IP;

\item
  update a record in a transaction.

\end{itemize}

Let's express the worker in terms of Mount. The task runs in a separate thread, so we need a thread or future with an infinite loop. In order to stop the process on request, the loop must be conditional, with a flag check at each step. The flag is available both to the worker and to the one who controls it.

\index{futures!implementation}

In Clojure, this is solved using a future and atom. The atom stores the flag that is a sign of the continuation of the loop. At every step, a future dereferences the atom, and if it is true, executes the task. To complete the future, take two actions. First, set the flag to false. Second, wait until the future becomes \emph{realized}. In Clojure, this means that the executor has completed the task and responded to the future.

Let's prepare a worker module. You will need a config, database, logging, and HTTP client:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.mount.worker
  (:require
   [clojure.java.jdbc :as jdbc]
   [clj-http.client :as client]
   [clojure.tools.logging :as log]
   [book.systems.mount.db :refer [db]]
   [book.systems.mount.config
    :refer [config]]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.mount.worker
  (:require
   [clojure.java.jdbc :as jdbc]
   [clj-http.client :as client]
   [clojure.tools.logging :as log]
   [book.systems.mount.db :refer [db]]
   [book.systems.mount.config :refer [config]]))
  \end{clojure}
\end{english}

\fi

The worker is a map with the \verb|:flag| and \verb|:task| fields, a state, and a future. The \verb|:start| phase prepares this map. The \verb|make-task| function does not exist yet, but we believe that it will return a future. In the \verb|:stop| phase, the flag becomes false, and we wait for the future to stop.

\index{clojure.core!realized?}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defstate worker
  :start
  (let [{task-opt :worker} config
        flag (atom true)
        task (make-task flag task-opt)]
    {:flag flag :task task})
  :stop
  (let [{:keys [flag task]} worker]
    (reset! flag false)
    (while (not (realized? task))
      (log/info
      "Waiting for the task
                to complete")
      (Thread/sleep 300))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defstate worker
  :start
  (let [{task-opt :worker} config
        flag (atom true)
        task (make-task flag task-opt)]
    {:flag flag :task task})
  :stop
  (let [{:keys [flag task]} worker]
    (reset! flag false)
    (while (not (realized? task))
      (log/info "Waiting for the task to complete")
      (Thread/sleep 300))))
  \end{clojure}
\end{english}

\fi

The \verb|:start| and \verb|:stop| code should be small. Move technical steps into functions to make the code simpler. If you don't, the start and stop logic will be difficult to understand.

Let's add worker parameters to the EDN file. We need one field~--- how many milliseconds to wait between processing records.

\begin{english}
  \begin{clojure}
{:worker {:sleep 1000}}
  \end{clojure}
\end{english}

\index{functions!make-task}

Let's describe the \verb|make-task| function. It takes an atom with flag and EDN parameters. The function will return a future with a loop:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure/lines}
(defn make-task
  [flag opt]
  (let [{:keys [sleep]} opt]
    (future
      (while @flag
        (try
          (task-fn)
          (catch Throwable e
            (log/error e))
          (finally
            (Thread/sleep
              sleep)))))))
  \end{clojure/lines}
\end{english}

\else

\begin{english}
  \begin{clojure/lines}
(defn make-task
  [flag opt]
  (let [{:keys [sleep]} opt]
    (future
      (while @flag
        (try
          (task-fn)
          (catch Throwable e
            (log/error e))
          (finally
            (Thread/sleep sleep)))))))
  \end{clojure/lines}
\end{english}

\fi

The \verb|task-fn| function executes the business logic of the application \coderef{7}. We wrap it in \verb|try/catch| to prevent the future from crashing. If we caught an exception, we write it to the log. There is a slight delay at the end of the iteration to avoid the flurry of database requests \coderef{11}. If someone sets \verb|flag| false, control will exit from \verb|while| and the future will end.

Now let's describe the \verb|task-fn| function. It reads one record from the database, which is awaiting processing. We are looking for geodata by IP using the \verb|get-ip-info| function. We don't know yet how the search works, but we know that it will return a map with the \verb|city|, \verb|country|, and other fields.

\index{transactions}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure/lines}
(defn task-fn []
 (jdbc/with-db-transaction [tx db]
  (when-let [request
             (first
              (jdbc/query tx query))]
    (let [{:keys [id ip]} request
          info   (get-ip-info ip)
          {zip :postal_code
           country country_name
           city :city lat :lat
           lon :lng} info
          fields {:is_processed true
                  :zip zip
                  :country country
                  :city city
                  :lat lat
                  :lon lon}]
      (jdbc/update! tx :requests
        fields ["id = ?" id])))))
  \end{clojure/lines}
\end{english}

\else

\begin{english}
  \begin{clojure/lines}
(defn task-fn []
  (jdbc/with-db-transaction [tx db]
    (when-let [request (first (jdbc/query tx query))]
      (let [{:keys [id ip]} request
            info   (get-ip-info ip)
            fields {:is_processed true
                    :zip (:postal_code info)
                    :country (:country_name info)
                    :city (:city info)
                    :lat (:lat info)
                    :lon (:lng info)}]
        (jdbc/update! tx :requests
                      fields
                      ["id = ?" id])))))
  \end{clojure/lines}
\end{english}

\fi

\index{SQL!FOR UPDATE}

We moved the request to find a record to the \verb|query| variable. This shortens the \verb|task-fn| code. It is an SQL query with a \verb|FOR UPDATE| statement locking the record to change in other connections.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def query
  "SELECT * FROM requests
   WHERE NOT is_processed
   LIMIT 1 FOR UPDATE;")
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def query
  "SELECT * FROM requests WHERE NOT is_processed
   LIMIT 1 FOR UPDATE;")
  \end{clojure}
\end{english}

\fi

\verb|FOR UPDATE| works only in a transaction, so the function body is wrapped in \texttt{(jdbc/with\--db-transaction)}. It is a macro within which a transactional connection to the database is available. The \verb|tx| symbol points to it. We're passing \verb|tx|, not \verb|db|, to the JDBC function \ifx\DEVICETYPE\MOBILE(lines 5 and ~18)\else(lines 3 and 12)\fi.

\index{functions!get-ip-info}
\index{websites!iplocation.com}

Let's write the \verb|get-ip-info| function. It accesses a service that accepts an IP and returns information about it in JSON. In our case, this is the \verb|iplocation.com| site. In high-performance systems, address databases are deployed locally, so they do not depend on third-party services.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn get-ip-info [ip]
  (:body (client/post
           "https://iplocation.com"
           {:form-params {:ip ip}
            :as :json})))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn get-ip-info [ip]
  (:body (client/post "https://iplocation.com"
                      {:form-params {:ip ip}
                       :as :json})))
  \end{clojure}
\end{english}

\fi

\noindent
If we call \verb|get-ip-info| with the Berlin address, we get the following map:

\begin{english}
  \begin{clojure}
(get-ip-info "85.214.132.117")

{:postal_code "12529"
 :continent_code "EU"
 :region_name "Land Berlin"
 :city "Berlin"
 :isp "Strato AG"
 :region "BE"
 :country_code "DE"
 :country_name "Germany"
 :time_zone "Europe/Berlin"
 :lat 52.5167
 :company "Strato AG"
 :lng 13.4}
  \end{clojure}
\end{english}

We have described the last element of the worker, and it is ready to go. Let's add a few records to the database and start the worker. After a while, we'll read these records again.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(mount/start)

(jdbc/insert! db :requests
  {:path "/help" :ip "31.148.198.0"})
;; wait for a while

(jdbc/query db "select * from requests")
({:path "/help"
  :ip "31.148.198.0"
  :is_processed true
  :city "Pinsk"
  :zip "225710" :id 1
  :lon 26.0728 :lat 52.1214
  :country "Belarus"})
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(mount/start)

(jdbc/insert! db :requests {:path "/help" :ip "31.148.198.0"})
;; wait for a while

(jdbc/query db "select * from requests")
({:path "/help" :ip "31.148.198.0" :is_processed true
  :city "Pinsk" :zip "225710" :id 1
  :lon 26.0728 :lat 52.1214 :country "Belarus"})
  \end{clojure}
\end{english}

\fi

The address \verb|31.148.198.0| will resolve in Pinsk, the city of Belarus. The system is working properly.

\subsection{System Build}

\index{components}

The components are ready and working separately; all that remains is to put them together. To do this, we'll write a central module, \verb|core|, which will import all known components. The \verb|(mount/start)| call from it will start the entire system. Let's see why this module is needed.

The \verb|start| and \verb|stop| functions work nothing but on components known to Mount. If you load the worker module, then Mount gets information about \verb|worker|, \verb|db|, and \verb|config|, but not \verb|server|, because no one refers to it in \verb|require|. Therefore, the server module will not load, and the system will not know about this component. You can solve the problem by importing all components without exception.

\label{mount-main}

\begin{english}
  \begin{clojure}
(ns book.systems.mount.core
  (:require
   ;; other packages...
   [mount.core :as mount]
   book.systems.mount.config
   book.systems.mount.db
   book.systems.mount.server
   book.systems.mount.worker))

(defn start []
  (mount/start))
  \end{clojure}
\end{english}

You may notice that specifying the \verb|config| and \verb|db| modules is optional. The compiler will load them automatically because \verb|server| and \verb|worker| refer to them. We have specified them for clarity, and we recommend doing the same, especially if you are just getting started with Clojure. The \verb|core| module is a component registry: a mere glance at it is enough to understand how the project works. Indicate all components in it, without exception.

\subsection{Dependencies}

\index{dependencies}

The main system task is to traverse components in the correct order, taking into account the dependencies. Let's take a look at how Mount handles this.

We don't specify component dependencies when declaring it. \verb|Worker| needs \verb|config| and \verb|db|, but this is not stated anywhere. When we call \verb|(mount/start)|, the system guesses the startup order: \verb|config|~\arr{} \verb|db|~\arr{} \verb|worker|. If you swap any two elements, it will fail. How does it work?

Mount relies on the Clojure compiler to compute the order. Namespaces depend on each other, like components in a system. The compiler looks for references to other modules in the \verb|ns| body and loads them first. Let's remember what the worker's header looks like:

\begin{english}
  \begin{clojure}
(ns book.systems.mount.worker
  (:require
   [book.systems.mount.db :refer [db]]
   [book.systems.mount.config
     :refer [config]]))
  \end{clojure}
\end{english}

\index{graph of references}

\ifx\MODE\PRINT

\noindent
Let's draw a graph of references:

\begin{figure}[h!]
  \chart{chart-sys-2}
\end{figure}

\fi

\ifx\MODE\EBOOK

\noindent
Let's draw a graph of references \fig{fig:chart-sys-2}.

\begin{figure}[ht!]
  \chart{chart-sys-2}
  \caption{First level dependencies}
  \label{fig:chart-sys-2}
\end{figure}

\fi

The compiler will load \verb|mount.worker| only after resolving the dependencies. It will start with the database module, which simplified definition is as follows:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.mount.db
  (:require
   [book.systems.mount.config
    :refer [config]]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.mount.db
  (:require
   [book.systems.mount.config :refer [config]]))
  \end{clojure}
\end{english}

\fi

\ifx\MODE\PRINT

\noindent
From the compiler's point of view, it looks like this:

\begin{figure}[h!]
  \chart{chart-sys-3}
\end{figure}

\fi

\ifx\MODE\EBOOK

\noindent
From the compiler's point of view, it looks like this \fig{fig:chart-sys-3}:

\begin{figure}[b!]
  \chart{chart-sys-3}
  \caption{Зависимости второго уровня}
  \label{fig:chart-sys-3}
\end{figure}

\fi

Before loading the \verb|db|, the compiler takes care of \verb|config|. The latter is independent of other modules and will be loaded first. Then the compiler will return to\verb|db| and load it. After that, it will rise to the \verb|worker| level. The \verb|db| module is ready; \verb|config| is next on the list. We've loaded the configuration in the \verb|db| stage. Clojure doesn't load a module twice, so the compiler skips it. The last step will load \verb|worker|.

\index{namespaces}

We have deduced the order of the \emph{namespaces}: \verb|config|, \verb|db|, \verb|worker|. Each \verb|defstate| form is executed on the same queue. Therein lies a trick: the \verb|defstate| call increments the internal Mount counter, and the component remembers this number. The \verb|config|, \verb|db|, and \verb|worker| entities will be numbered 1, 2, and 3. Mount walks through components in ascending order to start the system and descending to stop it.

\subsection{Internal Structure}

Mount stores information about components in private atoms. They are hidden from consumers, but Clojure allows you to reach them. When the components are loaded, run:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def _state
  @@(resolve 'mount.core/meta-state))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def _state @@(resolve 'mount.core/meta-state))
  \end{clojure}
\end{english}

\fi

\index{clojure.core!resolve}

The \verb|_state| variable will contain a component map. The double \verb|@| operator plays the following role. The \verb|resolve| function will return a \verb|Var| object by a symbol. Earlier, we found out that the \verb|Var| object is a container that stores a value \page{var-section}. The first \verb|@| retrieves a value from \verb|Var|; it is an atom with a map. The second \verb|@| gets the map from the atom.

\mnoindent
The map's key is a text link to the component. In our case, it is \texttt{\#'book.systems.mount\-.config/config}. The value is another map with the state of the component. We are interested in the \verb|order| field, i.e., its number. Let's sort the components by it and get the correct order:

\begin{english}
  \begin{clojure}
(->> _state
     vals
     (sort-by :order)
     (map #(-> % :var meta :name)))
;; (config server db worker)
  \end{clojure}
\end{english}

The code below will return a map of running components with a similar structure:

\begin{english}
  \begin{clojure}
@@(resolve 'mount.core/running)
  \end{clojure}
\end{english}

The \verb|state-seq| atom stores a global component counter. To read it, run the following:

\begin{english}
  \begin{clojure}
@@(resolve 'mount.core/state-seq) ;; 4
  \end{clojure}
\end{english}

\noindent
Since our components (server, database, and others) have occupied values from 0 to 3, we get 4.

When working with Mount, you should not edit its contents. The examples above are needed so that you better understand the structure of this library.

\subsection{State}

\index{state}

The ease of changing a component when calling \verb|start| and \verb|stop| is like magic. Here's the secret behind \verb|defstate|: it works using the \verb|alter-var-root| function, which we covered in the chapter on mutability \page{alter-var-root}. Let's remember the server's component:

\begin{english}
  \begin{clojure}
(defstate server
  :start (let [{jetty-opt :jetty} config]
           (run-jetty app jetty-opt))
  :stop (.stop ^Server server))
  \end{clojure}
\end{english}

The \verb|def-state| form expands  into multiple expressions. These are the global variable with no value:

\index{variables!without value}

\begin{english}
  \begin{clojure}
(def server)
  \end{clojure}
\end{english}

\noindent
and anonymous start and stop functions. The function bodies will get the code from the \verb|:start| and \verb|:stop| keys.

\begin{english}
  \begin{clojure}
(fn [] ;; start
  (alter-var-root #'server
   (fn [_]
     (let [{jetty-opt :jetty} config]
       (run-jetty app jetty-opt)))))

(fn [] ;; stop
  (alter-var-root #'server
   (fn [_]
     (.stop ^Server server))))
  \end{clojure}
\end{english}

Mount places references to these functions in the \verb|meta-state| atom. To start a component, you need to find a start function in the map and call it. The function will assign a new value to the \verb|#'server| variable. Stopping works the same way.

\subsection{Selective Startup}

\label{mount-selective}
\index{systems!selective startup}

So far, we have run the entire system. The call \verb| (mount/start)| without parameters runs through the \verb|meta-state| and enables all components. This is not always convenient. Let's say we are working on a worker and would like to run only it and its dependencies. In this case, we do not need a webserver.

To start only the required components, their references are passed to the \verb|mount/start| function. Note: This function expects precisely a reference, a \verb|Var| object.

\begin{english}
  \begin{clojure}
(mount/start
  #'book.systems.mount.config/config
  #'book.systems.mount.db/db
  #'book.systems.mount.worker/worker)
  \end{clojure}
\end{english}

If you pass a value without \verb|#'|, Mount will not start the component. The relationship between a variable and a value works one-way: the former cannot be found using the latter. In the example below, there will be no error; nothing will happen:

\begin{english}
  \begin{clojure}
;; does nothing
(mount/start
  book.systems.mount.config/config
  book.systems.mount.db/db
  book.systems.mount.worker/worker)
  \end{clojure}
\end{english}

\index{variables!Var}

Newbies are confused by the fact that the function expects \verb|Var|, not a value. This is not obvious since variables are rarely used in Clojure.

With a manual startup, you are responsible for the order of the components. Let's say you forgot that the database and the worker need a configuration:

\begin{english}
  \begin{clojure}
(mount/start
  #'book.systems.mount.db/db
  #'book.systems.mount.worker/worker)
  \end{clojure}
\end{english}

We'll get a weird exception. It will be thrown in the \verb|db| component where the pool is created. The \verb|config| object is not running, and the expression \spverb|(:pool config)| will return \verb|nil|. If we try to create a pool from nil, we get \verb|NPE|.

As the system grows, it becomes more difficult to track dependencies. Mount only knows the order of the components, not how they are related. This is the weak point of this library~--- programmers have to specify components manually to enable the subsystem. To improve this scenario, the library offers component \emph{selectors}. These are functions that return references to components by some hallmark.

For example, the \verb|except| selector will return component names \emph{other than} those listed. If we pass the result into \verb|start|, we get a system without the specified components. Below, a subset is enabled without a web server:

\begin{english}
  \begin{clojure}
(-> [#'book.systems.mount.server/server]
    mount/except
    mount/start)
  \end{clojure}
\end{english}

For other selectors and their combinations, see the project's GitHub page. In addition to selectors, Mount offers functions to invert the system: to enable what is not working and vice versa.

\subsection{Reboot problem}

When working with a project, we connect to it from the editor via the REPL. To make the Lisp machine state match the code, we execute the changed sections on the server. Question: what happens if you fix a component that is already running? How will Mount react to module reboots?

\index{Emacs}
\index{CIDER}

If you are using Emacs and CIDER, connect to the project via \verb|M-x cider-connect|. Start the system as we did above. Open the server module and execute \verb|M-x cider-eval-buffer| (or with keyboard buttons \verb|C-c C-k|). The command will execute the file on the server. All definitions, including \verb|ns|, \verb|def|, and \verb|defstate, will trigger again.

A message will appear saying that the server rebooted. The \verb|defstate| macro checks if a component with that name exists and is running. Mount will stop the component and start it with new versions of \verb|start| and \verb|stop|.

Rebooting is not always the desired behavior. With frequent changes, "out of sync" occurs~--- a situation when a component is considered disabled, but its resource is busy. For example, in the \verb|:stop| block, we did not call the \verb|(.stop)| method. If we restart such a component, we get an error that the port is busy.

The component's response to reloading is indicated in metadata. It is the \verb|:on-reload| field, which by default is \verb|:restart|. With it, the component restarts itself when \verb|defstate| is called again. If you set \verb|:stop|, the component will stop. The \verb|:noop| key means "do nothing." The component with metadata looks like this:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defstate
  ^{:on-reload :noop}
  server
  :start (run-jetty app
           {:join? false :port 8080})
  :stop (.stop server))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defstate
  ^{:on-reload :noop}
  server
  :start (run-jetty app {:join? false :port 8080})
  :stop (.stop server))
  \end{clojure}
\end{english}

\fi

If unsure, choose \verb|noop|; with it, loading code into the REPL has no side effects. Changes to the code are not always component-related: there may be a typo or a comment. If you modified the component, restart it manually.

\subsection{Independent Work}

\index{libraries!Clj-http}

Let's go back to the \verb|get-ip-info| function from the worker module. For each call, it makes an HTTP request. At a low level, we open a TCP connection, work with it, and close it. That is not optimal, and the problem is solved as with databases by using a connection pool. Examine the following example from the \footurl{Clj-http}{https://github.com/dakrone/clj-http}[Clj-http][-10mm] library:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(:require
 [clj-http.conn-mgr :refer
   [make-reusable-conn-manager
    shutdown-manager]])

;; create a new pool
(def cm (make-reusable-conn-manager
         {:timeout 2 :threads 3}))

;; make a request within the pool
(client/get "http://example.org/"
            {:connection-manager cm})

;; shut down the pool
(shutdown-manager cm)
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
;; create a new pool
(def cm (clj-http.conn-mgr/make-reusable-conn-manager
         {:timeout 2 :threads 3}))

;; make a request within the pool
(client/get "http://example.org/"
            {:connection-manager cm})

;; shut down the pool
(clj-http.conn-mgr/shutdown-manager cm)
  \end{clojure}
\end{english}

\fi

\index{connection pool!in HTTP}

Write a component that sends requests through the pool. Component parameters (timing, number of threads) come from the config. At the start, the component opens the pool and closes it upon stopping. Modify the worker to depend on the new component.

\section{Component}

\index{libraries!Component}

The \footurl{Component}{https://github.com/stuartsierra/component}[Compo-\\*nent][1mm] library also describes a system and components. It is a small framework where the main thing is the idea, rather than the code amount. The design of the Component library is fundamentally different from Mount, which we have already discussed.

\index{state}
\index{components}

Here, as in Mount, the \verb|start| and \verb|stop| operations act on a component. They return a \emph{copy} of the object in a new state; the original component remains unchanged. You can say that components are immutable. This cuts off errors related to global variables.

A system is a map of components with dependencies. Initially, the system is at rest; its components are not running. The startup code walks through and enables them. The result is a working copy of the system. It is similar to the original one, but each component has been replaced with its work version. Stopping works the same way: the output will be a disabled copy of the system.

Global state is illegal in Component. The library has no hidden atoms to account for components. One component can only access another if they are dependent. Like a system, a component avoids atoms and other mutable types. For each action, it generates a new copy of itself.

\subsection{How It Works}

\label{defrecord}

A component is an object that implements the \verb|Lifecycle| protocol. The protocol includes the \verb|start| and \verb|stop| methods. A typed map that is declared in the \verb|defrecord| form can be a component. They are also called “typed records” or “records”.

\index{clojure.core!defrecord}
\index{records}
\index{protocols!Lifecycle}

A record differs from a dictionary in that it lists keys in advance. These are called \emph{slots} of the record. Slots work faster than the keys of a regular map. The component needs them for input parameters and state.

Records and protocol fit together at the language level. When declaring a record, you can immediately expand it with the protocol. In protocol methods, slots are available as local variables. This way, a programmer can reduce the amount of code and save time.

\index{OOP}

A component hides its state, and only it knows how to control it. It would be a mistake to read its slots and pass them to functions. Use the methods included in the component protocol. In Component, code is somewhat similar to OOP: it is an object and a set of operations. Like a class, you can initiate, start, and stop a component.

\index{OOP!SOLID}

The difference is that the components are immutable. The transition to a new stage will not affect the old component, while in classical languages, its fields would be rewritten. The \verb|SOLID| principle and the encapsulation, inheritance, polymorphism triple do not have the same power in Clojure. Most of them are unnecessary. When programming in Clojure, we might not adhere to OOP postulates.

\subsection{The First Component}

Let's rewrite our system from the previous section with Component. We'll start with a web server. In the \verb|server.clj| file, declare the namespace:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.comp.server
  (:require
   [com.stuartsierra.component
    :as component]
   [ring.adapter.jetty
    :refer [run-jetty]]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.comp.server
  (:require
   [com.stuartsierra.component :as component]
   [ring.adapter.jetty :refer [run-jetty]]))
  \end{clojure}
\end{english}

\fi

A component is a record with two slots~--- \verb|:options| and \verb|:server|. Options contain the Jetty server's parameters, and \verb|server| contains its instance. The \verb|component/Lifecycle| line stands for the protocol that implements the record. Below is the \emph{implementation} of the protocol.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defrecord Server [options server]

  component/Lifecycle

  (start [this]
    (let [server (run-jetty app
                   options)]
      (assoc this :server server)))

  (stop [this]
    (.stop server)
    (assoc this :server nil)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defrecord Server [options server]

  component/Lifecycle

  (start [this]
    (let [server (run-jetty app options)]
      (assoc this :server server)))

  (stop [this]
    (.stop server)
    (assoc this :server nil)))
  \end{clojure}
\end{english}
\fi

The \verb|start| method will return the same record, but with the full \verb|:server| slot. It contains the server object. The \verb|stop| method takes a running component. It terminates the server and returns another record in which the \verb|:server| slot is \verb|nil|.

\index{initialization}

Inside the methods, we refer to slots as local variables. This only works if the methods are declared inside \verb|defrecord|. If you extend the record with a separate step, for example, via \verb|extend|, access to the slots will be lost. In this case, you have to extract them from the \verb|this| variable.

The \verb|Server| entity is not a component but just an abstract description. In the first step, it \emph{is initiated}, that is, an instance of it is created. To do this, use the \verb|map-> <Record>| function, where \verb|<Record>| is the name of the record. The \verb|defrecord| macro automatically creates this function. In our case, it is named \verb|map-> Server|. The function takes a regular map and returns its typed version. The keys of a map correspond to the record slots. If the key does not exist, the slot is \verb|nil|.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def s-created
  (map->Server
    {:options {:port 8080
               :join? false}}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def s-created
  (map->Server {:options {:port 8080 :join? false}}))
  \end{clojure}
\end{english}

\fi

The \verb|s-created| variable is an instance of the \verb|Server| record. We specified the \verb|options| slot, but not the \verb|server| one because it will be filled out later.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def s-started
  (component/start s-created))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def s-started (component/start s-created))
  \end{clojure}
\end{english}

\fi

\mnoindent
This expression will return the \emph{running} component. Open your browser at \texttt{http://127.0.0.1\-:8080} and check if the server is working. The \verb|s-started| record has the \verb|:server| slot filled:

\begin{english}
  \begin{clojure}
(-> s-started :server type)
;; org.eclipse.jetty.server.Server
  \end{clojure}
\end{english}

Stop the component. Make sure that the page no longer opens and the server slot is \verb|nil|.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def s-stopped
  (component/stop s-started))
(:server s-stopped) ;; nil
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def s-stopped (component/stop s-started))
(:server s-stopped) ;; nil
  \end{clojure}
\end{english}

\fi

We went through the full life cycle of the component: preparation, start-up, and shutdown. The transition to each stage gives a new component. In practice, the components are not manually controlled; the system controls them.

\subsection{Constructor}

\index{constructor}

Let's recall how we created the \verb|Server| instance.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(map->Server
  {:options {:port 8080
             :join? false}})
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(map->Server {:options {:port 8080 :join? false}})
  \end{clojure}
\end{english}

\fi

\index{slots}

This entry has a drawback: you need to remember which slots are for initialization and which are for internal state. This is not critical for simple records, but, in practice, there are components with ten or more slots. To avoid confusion, declare a \emph{constructor} function.

The constructor takes only arguments needed for the component initialization. In our case, it's \verb|options|, so the function looks like this:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn make-server
  [options]
  (map->Server {:options options}))

(def s-created
  (make-server {:port 8080
                :join? false}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn make-server
  [options]
  (map->Server {:options options}))

(def s-created (make-server {:port 8080 :join? false}))
  \end{clojure}
\end{english}

\fi

The constructor makes it easier to create the component: it is impossible to pass something superfluous to \verb|map->Server|. A constructor is a function; so you can add documentation and spec to it. An advanced editor will suggest a signature at the place of a call. I recommend you to write a constructor even for trivial components.

\subsection{Slots feature}

When the server stops, we do two things: we call the method \verb|(.stop)| and replace the slot with \verb|nil|. Why not replace \verb|assoc| with \verb|dissoc|? Why store \verb|nil| when you can detach a field?

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
;; why this?
(assoc this :server nil)
  \end{clojure}

\splitter

  \begin{clojure}
;; but not this?
(dissoc this :server)
  \end{clojure}
\end{english}

\else

\begin{english}

\noindent
\begin{tabular}{ @{}p{5cm} @{}p{5cm} }

  \begin{clojure}
;; why this?
(assoc this :server nil)
  \end{clojure}

&

  \begin{clojure}
;; but not this?
(dissoc this :server)
  \end{clojure}

\end{tabular}

\end{english}

\fi

The reason is in the way records and slots are arranged. A record retains its unique properties as long as its slots are in place. If we take a slot away from a record via \verb|dissoc|, we get a regular map. Let's show this with an example:

\begin{english}
  \begin{clojure}
(-> s-stopped
    (assoc :server nil)
    type)
;; book.systems.comp.server.Server

(-> s-stopped
    (dissoc :server)
    type)
;; clojure.lang.PersistentArrayMap
  \end{clojure}
\end{english}

If a component calls \verb|dissoc| on itself~--- we'll get a map instead of the component at a new stage. This leads to a strange behavior: when you try to stop the component, it will continue to work. Why does it happen?

When a record is extended by a protocol, it forms a relationship between the class and the implementation. For \verb|Server|, the \verb|start| and \verb|stop| methods do one thing, and for \verb|DB| or \verb|Worker|~--- another.

Finding a method in Clojure works in a class hierarchy similar to catching exceptions \page{exc-hierarchy}. When loaded, Component extends the base \verb|Object| class with the \verb|start| and \verb|stop| methods, which only return \verb|this|. If you take a slot away from a record, it becomes a map. The map does not directly implement \verb|Lifecycle|; therefore, the search will go up the inheritance tree. As a result, it resolves to \verb|Object|, which will return \verb|this| by default.

Below is an example of a failed component. Its \verb|start| method will return a map with the \verb|server| field:

\begin{english}
  \begin{clojure}
(defrecord BadServer [options server]
  component/Lifecycle
  (start [this]
    {:server (run-jetty app options)})
  (stop [this]
    (.stop server)
    nil))
  \end{clojure}
\end{english}

The server will start without errors, but then a problem awaits us. If we pass the result of \verb|start| to \verb|stop|, the server will not stop. No matter how many times you call \verb|component/stop|, nothing will happen.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def bs-created
  (map->BadServer
    {:options {:port 8080
               :join? false}}))

(def bs-started
   (component/start bs-created))
(type bs-started)
;; clojure.lang.PersistentArrayMap

(component/stop bs-started)
;; does nothing, the server still works
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def bs-created (map->BadServer
                  {:options {:port 8080 :join? false}}))

(def bs-started (component/start bs-created))
(type bs-started)
;; clojure.lang.PersistentArrayMap

(component/stop bs-started)
;; does nothing, the server still works
  \end{clojure}
\end{english}

\fi

An error also crept into the stop method. When stopped, the component will terminate the server but return \verb|nil|. If you pass the result of \verb|stop| to \verb|start|, the latter gets \verb|nil|. This will throw an exception because \verb|nil| does not implement the \verb|Lifecycle| protocol.

\index{protocols!Lifecycle}

Make sure that the component changes only the values of the slots rather than their structure. Typically, the \verb|start| and \verb|stop| code ends with the \verb|assoc| form, but not \verb|dissoc|. If the condition meets, there should be no problems with the component forwarding by its stages:

\begin{english}
  \begin{clojure}
(-> (make-component {...})
    (component/start)
    (component/stop)
    (component/start))
  \end{clojure}
\end{english}

\index{forwarding}

\subsection{Database Component}

Let's write a component to work with a database. It contains the \verb|options| and \verb|db-spec| slots. The former is a map of future pool options, and the latter is an open-pool JDBC spec. You know how it works from the previous sections.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defrecord DB [options db-spec]
  component/Lifecycle
  (start [this]
    (let [pool (cp/make-datasource
                 options)]
      (assoc this :db-spec
        {:datasource pool})))
  (stop [this]
    (-> db-spec
        :datasource
        cp/close-datasource)
    (assoc this :db-spec nil)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defrecord DB [options db-spec]
  component/Lifecycle
  (start [this]
    (let [pool (cp/make-datasource options)]
      (assoc this :db-spec {:datasource pool})))
  (stop [this]
    (-> db-spec :datasource cp/close-datasource)
    (assoc this :db-spec nil)))
  \end{clojure}
\end{english}

\fi

\noindent
Let's add a constructor:

\begin{english}
  \begin{clojure}
(defn make-db [options]
  (map->DB {:options options}))
  \end{clojure}
\end{english}

Now the component is ready to start: it can be run through the \verb|make-db|~\arr{} \texttt{component\-/start}~\arr{} \verb|component/stop| functions.

It is not yet clear how to execute a request through this component. We are interested in the \verb|db-spec| slot, which stores the spec. You can isolate it and pass it to the function:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(let [{:keys [db-spec]} db-started
      users (jdbc/query db-spec
              "select * from users")]
  (process-users users))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(let [{:keys [db-spec]} db-started
      users (jdbc/query db-spec "select * from users")]
  (process-users users))
  \end{clojure}
\end{english}

\fi

But this is an invalid approach: you must not invade a component, even if the language offers such a possibility. That violates the idea that the component is impartible for the consumer. We can only pull the "controls" that the component offers. Now the controls are not there, so let's add them.

Let's expand the \verb|DB| record with methods for working with the database. Let's put them in a separate protocol. Signatures are similar to JDBC functions, with the difference that the first parameter is the \verb|this| component, not a spec:

\index{protocols!IDB}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defprotocol IDB
  (query [this sql-params])
  (update!
    [this table set-map where-clause]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defprotocol IDB
  (query [this sql-params])
  (update! [this table set-map where-clause]))
  \end{clojure}
\end{english}

\fi

In the body of \verb|defrecord|, right after \verb|stop|, we implement the new protocol. Methods boil down to JDBC functions, into which we pass the \verb|db-spec| slot and arguments. At first, the component implements \verb|Lifecycle| as usual:

\begin{english}
  \begin{clojure}
(defrecord DB [options db-spec]
  ;; ...component/Lifecycle
  \end{clojure}
\end{english}

\noindent
A private protocol follows:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
  IDB
  (query [this sql-params]
    (jdbc/query db-spec sql-params))

  (update!
    [this table set-map where-clause]
    (jdbc/update! db-spec
      table set-map where-clause)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
  IDB
  (query [this sql-params]
    (jdbc/query db-spec sql-params))

  (update! [this table set-map where-clause]
    (jdbc/update! db-spec table set-map where-clause)))
  \end{clojure}
\end{english}

\fi

The component is ready for requests. We are not calling JDBC functions but protocol methods. This will isolate the dependency on JDBC and reduce code coupling.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def db-created (make-db options))
(def db-started
  (component/start db-created))

(query db-started "select * from users")
(update! db-started
  :users {:name "Ivan"} ["id = ?" 42])

(def db-stopped
  (component/stop db-started))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def db-created (make-db options))
(def db-started (component/start db-created))

(query db-started "select * from users")
(update! db-started :users {:name "Ivan"} ["id = ?" 42])

(def db-stopped (component/stop db-started))
  \end{clojure}
\end{english}

\fi

\subsection{Transactional Component}

\index{transactions}

Transactions are needed for consistent changes in a database. So far, we've used the \verb|jdbc/with-db-transaction| macro. It makes a transactional connection out of a regular one and assigns it to a symbol.

Unlike JDBC, our macro works with a component. It takes a regular component and assigns its \emph{transactional} version to the symbol. The macro performs the following steps:

\begin{itemize}

\item
  get a spec from a component;

\item
  wrap body in JDBC macro, assign the transactional connection to a variable;

\item
  get the component in which this variable replaces the \verb|:db-spec| slot;

\item
  assign a new component to a symbol from a macro.

\end{itemize}

\begin{listing}[ht!]

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defmacro with-db-transaction
  [[comp-tx comp-db & trx-opt] & body]
  `(let [{db-spec# :db-spec} ~comp-db]
     (jdbc/with-db-transaction
       [t-conn# db-spec# ~@trx-opt]
       (let [~comp-tx
             (assoc ~comp-db
               :db-spec t-conn#)]
         ~@body))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defmacro with-db-transaction
  [[comp-tx comp-db & trx-opt] & body]
  `(let [{db-spec# :db-spec} ~comp-db]
     (jdbc/with-db-transaction
       [t-conn# db-spec# ~@trx-opt]
       (let [~comp-tx (assoc ~comp-db :db-spec t-conn#)]
         ~@body))))
  \end{clojure}
\end{english}

\fi

\caption{Transaction macro for the database component}
\label{fig:trx-macro}

\end{listing}

\index{OOP!getter}
\index{OOP!setter}

See the macro code in a separate listing \ref{fig:trx-macro}.

When analyzing, you will notice that the principle of component closedness is violated. We read and manually replace the \verb|db-spec| slot. Think about how to improve the code. Hint: You can move access to the slot into the \verb|get-| and \verb|set-spec| methods, that is, the usual "getter" and "setter" from the OOP world. The difference is that the "setter" will return a new component. Here is the macro in action:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(with-db-transaction
  [db-tx db-started]
  (let [query "select * from requests
               limit 1 for update"
        result (query db-tx query)]
    (when-let [id (some->
                    result first :id)]
      (update! db-tx :requests
               {:is_processed false}
               ["id = ?" id]))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(with-db-transaction
  [db-tx db-started]
  (let [query "select * from requests limit 1 for update"
        result (query db-tx query)]
    (when-let [id (some-> result first :id)]
      (update! db-tx :requests
               {:is_processed false}
               ["id = ?" id]))))
  \end{clojure}
\end{english}

\fi

\noindent
In the PostgreSQL logs, we will see the following entries:

\index{SQL!BEGIN}
\index{SQL!UPDATE}
\index{SQL!COMMIT}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{sql}
BEGIN
select * from requests
limit 1 for update
UPDATE requests SET is_processed = $1
WHERE id = $2
DETAIL:  parameters: $1 = 'f', $2 = '3'
COMMIT
  \end{sql}
\end{english}

\else

\begin{english}
  \begin{sql}
BEGIN
select * from requests limit 1 for update
UPDATE requests SET is_processed = $1 WHERE id = $2
DETAIL:  parameters: $1 = 'f', $2 = '3'
COMMIT
  \end{sql}
\end{english}

\fi

\noindent
\verb|SELECT| and \verb|UPDATE| queries were indeed in a transaction.

\subsection{Worker}

Let's write a worker component. Let's declare the module and dependencies:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.comp.worker
  (:require
   [com.stuartsierra.component
    :as component]
   [book.systems.comp.db :as db]
   [clj-http.client :as client]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.comp.worker
  (:require
   [com.stuartsierra.component :as component]
   [book.systems.comp.db :as db]
   [clj-http.client :as client]))
  \end{clojure}
\end{english}

\fi

\index{protocols!IWorker}

\mnoindent
A worker is a record with \verb|Lifecycle| and \verb|IWorker| protocols. You are already familiar with the \verb|Lifecycle| protocol: it is the \verb|start| and \verb|stop| functions. Let's put the component logic in \verb|IWorker|. The logic includes an infinite loop and its preparation. We expect \verb|task-fn| to be a function that the worker calls at each step of the loop. The \verb|make-task| method wraps the function in the loop and \verb|try/catch|.

\begin{english}
  \begin{clojure}
(defprotocol IWorker
  (make-task [this])
  (task-fn [this]))
  \end{clojure}
\end{english}

The record stores four slots: input options, a continue flag, a future with a loop (\verb|task|), and a database. The fourth slot is the dependent component, the \verb|DB| instance we just wrote. Let's implement \verb|Lifecycle|:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure/lines}
(defrecord Worker
  [options flag task db]

  component/Lifecycle

  (start [this]
    (let [flag (atom true)
          this (assoc this :flag flag)
          task (make-task this)]
      (assoc this :task task)))

  (stop [this]
    (reset! flag false)
    (while (not (realized? task))
      (log/info
        "Waiting for
             the task to complete")
      (Thread/sleep 300))
    (assoc this :flag nil :task nil)))
  \end{clojure/lines}
\end{english}

\else

\begin{english}
  \begin{clojure/lines}
(defrecord Worker
  [options flag task db]

  component/Lifecycle

  (start [this]
    (let [flag (atom true)
          this (assoc this :flag flag)
          task (make-task this)]
      (assoc this :task task)))

  (stop [this]
    (reset! flag false)
    (while (not (realized? task))
      (log/info "Waiting for the task to complete")
      (Thread/sleep 300))
    (assoc this :flag nil :task nil)))
  \end{clojure/lines}
\end{english}

\fi

Notice line 8: we're adding a status flag to \verb|this|. The \verb|make-task| method expects \verb|this| with a filled \verb|flag| slot. If we remove this line, the \verb|make-task| will get an entry with an empty flag.

\index{functions!make-task}

Let's describe the \verb|IWorker| protocol. You are already familiar with the \verb|make-task| and \verb|task-fn| code from the Mount section. The difference is that now we are working with methods instead of functions. The method has direct access to slots, so you don't need to pass them in parameters. Below we moved the \verb|make-task| and \verb|task-fn| code into the component. For the sake of shortness, we will skip error catching and some details.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defrecord Worker
  ;; ...component/Lifecycle
  IWorker

  (make-task [this]
    (future
      (while @flag    ;; ...try/catch
        (task-fn this)))) ;; ...sleep

  (task-fn [this]
    (db/with-db-transaction [tx db]
      (when-let [[request]
                 (db/query tx query)]
        ;; get fields
        (let [fields (...))]
          (db/update! tx :requests
            fields ["id = ?" id]))))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defrecord Worker
  ;; ...component/Lifecycle
  IWorker

  (make-task [this]
    (future
      (while @flag        ;; ...try/catch
        (task-fn this)))) ;; ...sleep

  (task-fn [this]
    (db/with-db-transaction [tx db]
      (when-let [[request] (db/query tx query)]
        (let [fields (...))] ;; get fields
          (db/update! tx :requests
                      fields ["id = ?" id]))))))
  \end{clojure}
\end{english}

\fi

\noindent
Let's add a constructor, and the component will be ready:

\index{constructor}

\begin{english}
  \begin{clojure}
(defn make-worker
  [options]
  (map->Worker {:options options}))
  \end{clojure}
\end{english}

\subsection{Manual dependencies}

\index{dependencies!manual}

The worker differs from other components in dependencies. It is not yet clear how it knows about the database because the constructor accepts only options. The system solves this problem rather than a programmer. We should not pass components to each other when creating.

But for educational purposes, we \emph{will break} this rule and assemble the mini-system manually. This way helps us better understand how it works and test the code. Let's run an experiment in the \verb|core| module with two components. Add constructors to the module:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(ns book.systems.comp.core
  (:require
   [com.stuartsierra.component
    :as component]
   [book.systems.comp.worker
    :refer [make-worker]]
   [book.systems.comp.db
    :refer [make-db]]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(ns book.systems.comp.core
  (:require
   [com.stuartsierra.component :as component]
   [book.systems.comp.worker :refer [make-worker]]
   [book.systems.comp.db :refer [make-db]]))
  \end{clojure}
\end{english}

\fi

Take a look at the system below. It is a function that takes a config. We manually start the database and worker and return the component map. In the tenth line, we set the slot with the database for the worker.
Pay attention when to do it~--- the database component \emph{has already been enabled}, but the worker hasn't yet (because \verb|assoc| comes before \verb|start|).

\begin{english}
  \begin{clojure/lines}
(defn my-system-start
  [config]
  (let [{db-opt :pool
         worker-opt :worker} config
        db (-> db-opt
               make-db
               component/start)
        worker (-> worker-opt
                   make-worker
                   (assoc :db db)
                   component/start)]
    {:db db :worker worker}))
  \end{clojure/lines}
\end{english}

To start the system, pass the pool and worker parameters to the function. Save the system to a variable for later termination.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def _sys
  (my-system-start
    {:pool {...} :worker {...}}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def _sys (my-system-start {:pool {...} :worker {...}}))
  \end{clojure}
\end{english}

\fi

Test the system: add records to the \verb|requests| table and make sure that the worker adds data to their fields. The stop function will stop the components in reverse order:

\begin{english}
  \begin{clojure}
(defn my-system-stop
  [system]
  (-> system
      (update :worker component/stop)
      (update :db component/stop)))

(my-system-stop _sys)
  \end{clojure}
\end{english}

\subsection{High-performance System}

\index{systems}

Let's consider how a system is built in practice. The \verb|system-map| function takes a chain of values. The odd elements are the component names, and the even ones are the calls of their constructors. The \verb|system-map| call builds a component tree with filled slots. We get the system at rest.

\index{functions!system-map}

When building the system, there should be no side effects. Constructors only create records. If a constructor is accessing a disk or changing state, this is a blunder.

The system depends on the config, so assembling is moved to the \verb|make-system| function. It takes a config map and divides it into parts. Each constructor takes its part. It is convenient when the config structure repeats the system with the component keys are at the top level, and option maps are below them.

To pass dependencies to the component, it is wrapped in the \verb|component/using| function. The second argument is the keys of the components, which it should receive before starting. The key can be a vector or a map. If the slot and component names match, it is a vector. If they differ, pass the map of the form \verb|{:slot :component}|.

Below, the \verb|make-system| function builds the system we agreed on at the beginning of the chapter. The \verb|worker| component is wrapped in \verb|component/using|. We passed the \verb|[:db]| vector because this is the name of both the worker slot and the system component.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn make-system
  [config]
  (let [{:keys [jetty pool worker]}
        config]
    (component/system-map
     :server (make-server jetty)
     :db     (make-db pool)
     :worker (component/using
               (make-worker worker)
               [:db]))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn make-system
  [config]
  (let [{:keys [jetty pool worker]} config]
    (component/system-map
     :server (make-server jetty)
     :db     (make-db pool)
     :worker (component/using
              (make-worker worker) [:db]))))
  \end{clojure}
\end{english}

\fi

\index{dependencies!Component}

\noindent
If the component name were \verb|:storage|, we would define a map:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(component/system-map
 :server  (make-server jetty)
 :storage (make-db pool)
 :worker  (component/using
            (make-worker worker)
            {:db :storage}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(component/system-map
 :server  (make-server jetty)
 :storage (make-db pool)
 :worker  (component/using
           (make-worker worker) {:db :storage}))
  \end{clojure}
\end{english}

\fi

Maps are useful for third party components, because their creators don't know what the entities in your project are called. Suppose the third-party component depends on \verb|:database|, but we have \verb|:db|. The map removes the name discrepancy problem.

To start the system, it is passed into \verb|component/start|. The system has a special \verb|Lifecycle| implementation. At startup, it builds a dependency graph and determines the traversal order. Before starting a component with dependencies, the system will pass them to the component through \verb|assoc|, as we did manually. Stopping the system works the same way.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def config {...})
(def sys-init (make-system config))
(def sys-started
  (component/start sys-init))
(def sys-stopped
  (component/stop sys-started))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def config {...})
(def sys-init (make-system config))
(def sys-started (component/start sys-init))
(def sys-stopped (component/stop sys-started))
  \end{clojure}
\end{english}

\fi

Like a component, the system flows freely through the \verb|start| and \verb|stop| functions in the \arr operator. If not, there is an error in the system.

\begin{english}
  \begin{clojure}
(-> (make-system config)
    (component/start)
    (component/stop)
    (component/start))
  \end{clojure}
\end{english}

\subsection{System Storage}

\index{systems!storage}

Above, we set the system using \verb|def|, which is not entirely correct. The system is an entity that we turn on on-demand. Treat it like a global variable that changes its value. \verb|Alter-var-root| is good for that.

A future system variable is declared in the module. The \verb|defonce| macro, which is executed strictly once, does this. Thanks to this, we will not lose the old system when reloading the module. Like a component, the system is in one of three states: rest, start, and stop. The \verb|system-init|, \verb|system-start|, and \verb|system-stop| functions bring the system to the required state. They work using the service \verb|alter-system|, which we specified using \verb|partial| for shortness.

\index{clojure.core!alter-var-root}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defonce system nil)

(def alter-system
  (partial alter-var-root #'system))

(defn system-init [config]
  (alter-system
    (constantly (make-system config))))

(defn system-start []
  (alter-system component/start))

(defn system-stop []
  (alter-system component/stop))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defonce system nil)

(def alter-system (partial alter-var-root #'system))

(defn system-init [config]
  (alter-system (constantly (make-system config))))

(defn system-start []
  (alter-system component/start))

(defn system-stop []
  (alter-system component/stop))
  \end{clojure}
\end{english}

\fi

\index{functions!-main}

Now there's a way to control the system. The \verb|main| function is the entry point to the program. It boils down to three steps: reading the configuration, preparing and starting the system.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn -main [& args]
  (let [config
        (load-config "config.edn")]
    (system-init config)
    (system-start)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn -main [& args]
  (let [config (load-config "config.edn")]
    (system-init config)
    (system-start)))
  \end{clojure}
\end{english}

\fi

The system is global, but we cannot access it directly. If a component is looking for something in the depths of the system, it means that the developer has failed. This approach destroys the very idea of a system and components. The system can only be accessed during development or testing. It is worth making the system private for higher reliability: this way, we will protect it from external access.

\index{clojure.core!defonce}
\index{variables!private}
\index{variables!one-time}

\begin{english}
  \begin{clojure}
(defonce ^:private system nil)
  \end{clojure}
\end{english}

Of course, you can also get to the private system with \verb|resolve| and a fully qualified symbol. This is an advanced technique that no variable can resist. But \verb|resolve| stands out, and trouble is easier to spot.

\subsection{Correct Completion}

\index{systems!completion}

The global system goes against what the library author writes: "In production, the system map is ephemeral. It is used to start all the components running, then it is discarded."

\index{graceful shutdown}

Let us disagree with the author. Practice shows that a reference to the system is also useful in production. Without the reference, a graceful application shutdown is impossible. Gracefulness means that all resources are closed.

During running, some components cannot stop at an arbitrary moment. These include task queues, data channels, and transactions. If a queue ends abnormally, we either lose the message or process it twice. Close resources properly, even if you have to wait for them.

\index{POSIX}
\index{signals!SIGTERM}

In production mode, the application listens for POSIX signals and responds to them appropriately. If SIGTERM comes, the application should stop the system, wait for the shutdown, and only then exit.

\index{libraries!Signal}

The \footurl{Signal}{https://github.com/pyr/signal}[Signal][1mm] library provides a macro to associate a signal with a reaction to it. Include the library in the project:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
;; project.clj
[spootnik/signal "0.2.2"]

;; src/book/systems/comp/core.clj
(ns ...
  (:require
   [signal.handler :refer
    [with-handler]]))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
;; project.clj
[spootnik/signal "0.2.2"]

;; src/book/systems/comp/core.clj
(ns ...
  (:require [signal.handler :refer [with-handler]]))
  \end{clojure}
\end{english}

\fi

Extend the \verb|-main| function. After the system starts, add an event to the SIGTERM and SIGHUP signals. The former stops the system and ends the program. We regard the latter as a system reboot.

\begin{english}
  \begin{clojure}
(with-handler :term
  (log/info "caught SIGTERM, quitting")
  (system-stop)
  (log/info "all components shut down")
  (System/exit)
  \end{clojure}
\end{english}

\index{signals!SIGHUP}

\begin{english}
  \begin{clojure}
(with-handler :hup
  (log/info "caught SIGHUP, reloading")
  (system-stop)
  (system-start)
  (log/info "system reloaded"))
  \end{clojure}
\end{english}

The signals do not work when the project is run via \verb|lein run| or in the REPL. To test the signals, build \verb|uberjar|, and run it as a Java application.

\begin{english}
  \begin{bash}
$ lein uberjar
$ java -jar target/book-standalone.jar
  \end{bash}
\end{english}

Press \verb|Ctrl+C|. The application will not end immediately, and you will see a message that all components have stopped.

Process control programs usually wait 30 seconds for a process to stop. Otherwise, it is terminated forcibly. The system wait should be within reasonable limits. If it goes beyond 30 seconds, you need to find and improve the problematic component.

\subsection{More about Waiting}

\index{systems!waiting}

Let's go back to the \verb|-main| function of the app. It is the entry point of the Clojure program:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn -main [& args]
  (let [config
        (load-config "config.edn")]
    (system-init config)
    (system-start)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn -main [& args]
  (let [config (load-config "config.edn")]
    (system-init config)
    (system-start)))
  \end{clojure}
\end{english}

\fi

If you are not familiar with the JVM intricacies, you may be wondering why the program does not terminate after calling \verb|(system-start)|? There is no loop, hook, or event behind it, but the platform continues to work.

\index{JVM!waiting}

This is the standard JVM behavior. If the program executed the instructions without error, the main thread waits until the child ones have stopped. System startup generates new threads (server, connection pool). After \verb|(system-start)|, the main thread will be running until they are finished. It will wait until the system is stopped in another thread or a termination signal, SIGTERM, arrives.

If the component is stateless, there will be no new threads. You can modify the \verb|DB| component so that the \verb|db-spec| slot is not a connection pool but a static map. Some components perform a one-time task at the start. If no components have created a thread, the program will execute \verb|start| for each one and exit.

\subsection{Improving Dependencies}

\index{dependencies!Component}

We passed dependencies to the component with \verb|using|:

\begin{english}
  \begin{clojure}
(component/system-map
 ;; ...
 :worker (component/using
          (make-worker worker) [:db]))
  \end{clojure}
\end{english}

When there are many components, dependencies make the code noisy and difficult to read. In the example above, we first get the component from the constructor and then add dependencies to it. The last step can be moved to the constructor. It will return a component with dependencies, and \verb|using| will leave the system. Let's rewrite the worker constructor:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn make-worker [config]
  (-> config
      map->Worker
      (component/using [:db])))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn make-worker [config]
  (-> config map->Worker (component/using [:db])))
  \end{clojure}
\end{english}

\fi

\noindent
The new system is cleaner and shorter:

\begin{english}
  \begin{clojure}
(component/system-map
 :server (make-server jetty)
 :db     (make-db pool)
 :worker (make-worker worker))
  \end{clojure}
\end{english}

Such an approach requires the component names to match the slot ones. If these are your components, agree on naming them with the team. It's easy to write your own constructor for third-party components.

\index{metadata!in Component}

Now let's look at how dependencies work. As you might guess, the \verb|component/using| call passes something to the component, but the latter doesn't change. It has no \verb|:deps| field or hidden atom. The component stores dependencies in \emph{metadata}.

Metadata is a map of additional information about an object. Metadata works with collections and some Clojure types (symbol, keyword, \verb|Var|). Metadata is used to supplement an object without changing it. Component dependencies are in line with this concept.

The \verb|meta| function returns the object's metadata. The code below proves that the constructor added dependencies:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(-> {...} make-worker meta)
#:...component{:dependencies {:db :db}}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(-> {...} make-worker meta)
#:com.stuartsierra.component{:dependencies {:db :db}}
  \end{clojure}
\end{english}

\fi

Another way to see them is to set the \verb|*print-meta*| variable to true. When the object is printed to the REPL, the metadata will appear:

\index{REPL}
\index{ printing!metadata}
\index{clojure.core!*print-meta*}
\index{clojure.core!set"!}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(set! *print-meta* true)

(make-worker {...})
^#:...component{:dependencies {:db :db}}
#book.systems.comp.worker.Worker{...}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(set! *print-meta* true)

(make-worker {...})
^#:com.stuartsierra.component{:dependencies {:db :db}}
#book.systems.comp.worker.Worker{...}
  \end{clojure}
\end{english}

\fi

\subsection{Grouping slots}

\index{slots!grouping}

Slots are divided into three groups: inputs, state, and dependencies. Let's recall the worker component:

\begin{english}
  \begin{clojure}
(defrecord Worker
    [options flag task db])
  \end{clojure}
\end{english}

In this example, the \verb|options| slot is for initialization, \verb|flag| and \verb|task| are for state, and \verb|db| is for dependency. The more complex the component is, the more slots are in each group. When slots are in random order, it's difficult to understand their semantics. It is considered good practice to separate groups of slots from each other with a comment:

\begin{english}
  \begin{clojure}
(defrecord Worker
    [;; init
     options
     ;; runtime
     flag
     task
     ;; deps
     db])
  \end{clojure}
\end{english}

The first is the \verb|init| group~--- the input parameters. The same arguments are expected from the constructor. The \verb|runtime| group lists the slots that the component will fill at startup. The \verb|deps| group indicates dependencies. They are the same as the key vector from the \verb|using| function.

Grouping slots improves code and makes it more readable. Agree with the whole team to implement this practice. When there are too many slots, that indicates the component is overcomplicated. In this case, you can move some of the logic into a child component and connect it depending on the first one.

We do not group slots here because, otherwise, the code will take up a lot of space. But be sure to do this in your real project.

\subsection{Conditional System}

\index{feature flags}

In the chapter on config, I wrote about \emph{feature flags} \page{feature-flags}. They are parameters that enable layers of logic. With flags, there is no need for a new build of the application. It is enough to change the configuration and restart the service.

Sometimes a system is not built linearly, but according to conditions. Let's say the worker is still in test mode. Let's add a field with the semantics "run worker" to the configuration. If the field is false, the system will start without this component.

\index{features}

Let's select the \verb|:features| group in the configuration for the features flags:

\begin{english}
  \begin{clojure}
{:features {:worker? true}
 :jetty {:join? false :port 8088}
 ...}
  \end{clojure}
\end{english}

Let's rewrite the \verb|make-system| function with flags. Before getting into \verb|system-map|, the components are selected. Let's select the essential components that always work and gradually add them.

The \verb|cond->| macro passes the vector through the chain of conditions and forms. If the \verb|worker?| condition is true, the following form will add the \verb|:worker| and \verb| (make-worker {...})| values to the vector. Other flags and expressions may appear below.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn make-system [config]
  (let [{:keys [features jetty
                pool worker]} config
        {:keys [worker?]} features
        comps-base [:server
                    (make-server jetty)
                    :db (make-db pool)]
        comps (cond-> comps-base
                worker?
                (conj :worker
                  (make-worker worker)))]
    (apply component/system-map comps)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn make-system [config]
  (let [{:keys [features jetty pool worker]} config
        {:keys [worker?]} features
        comps-base [:server (make-server jetty)
                    :db (make-db pool)]
        comps (cond-> comps-base
                worker?
                (conj :worker (make-worker worker)))]
    (apply component/system-map comps)))
  \end{clojure}
\end{english}

\fi

Let's make sure the flag works. Since a system is a record, so the \verb|keys| function will return its keys. The \verb|:worker| slot appears depending on the flag:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(keys (make-system
        {:features {:worker? false}}))
(:server :db)

(keys (make-system
        {:features {:worker? true}}))
(:server :db :worker)
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(keys (make-system {:features {:worker? false}}))
(:server :db)

(keys (make-system {:features {:worker? true}}))
(:server :db :worker)
  \end{clojure}
\end{english}

\fi

Flags make it easier to work with the project. Some components are complex and require special environments. If you can set them with a flag, it will benefit the whole team.

\subsection{System Descent}

\index{systems!descent}

Within the system, the components communicate freely with each other. If one of them needs the other, we'll add a dependency and a slot. The problem arises when another entity rather than a component accesses the system.

That mainly happens when an HTTP request handler needs individual components \page{first-handler}. A function does not fit well with the component ideas: the latter stores state, and the former avoids it. Starting and stopping the functions is meaningless. Component and function are opposite to each other.

Let's see a case where a database component is required in a query. How can you descend part of the system into a function without violating the principle of the library? We are not considering using a global variable because, in this case, it is surrender. Two methods will help~--- forwarding and closure.

\index{systems!forwarding}

\textbf{Forwarding} means passing individual components into a query. The variant has the right to exist because the request is a part of the server, and the server is a component. Based on this, the latter can add fields to the request. To make the database component available to the server, let's include it in dependencies. Let's change the server slots and the constructor:

\begin{english}
  \begin{clojure}
(defrecord Server
  [options server db]) ;; db added

(defn make-server
  [options]
  (-> (map->Server {:options options})
      (component/using [:db])))
  \end{clojure}
\end{english}

Let's extend the server \verb|start| method. If earlier we passed \verb|app| directly to \verb|run-jetty|, now we will add a new step. The \texttt{make\--hand\-ler} function wraps \verb|app| so that every query in \verb|app| is supplemented with a database.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
;; app factory
(defn make-handler [app db]
  (fn [request]
    (app (assoc request :db db))))

;; Lifecycle
(start [this]
  (let [handler (make-handler app db)
        server (run-jetty handler
                 options)]
    (assoc this :server server)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
;; app factory
(defn make-handler [app db]
  (fn [request]
    (app (assoc request :db db))))

;; Lifecycle
(start [this]
  (let [handler (make-handler app db)
        server (run-jetty handler options)]
    (assoc this :server server)))
  \end{clojure}
\end{english}

\fi

Let the home page display data from the database. The code below shows how to read a table from an HTTP request. In order not to complicate the example with HTML layout, let's return pretty printed data.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn app [{:keys [db]}]
  (let [data (db/query db
               "select * from requests")]
    {:status 200
     :body (with-out-str
             (clojure.pprint/pprint
               data))}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn app [{:keys [db]}]
  (let [data (db/query db "select * from requests")]
    {:status 200
     :body (with-out-str
             (clojure.pprint/pprint data))}))
  \end{clojure}
\end{english}

\fi

Over time, we will need other components, such as a task queue or cache. Let's do the same, add them to server dependencies and forward them to the \verb|make-handler|.

\index{systems!subset}

When there are too many components, it is inconvenient to store them at the top level of the request~--- there is a risk of keys conflicting. Let's put them in the nested \verb|:system| map. Note: there is not the entire system in \verb|:system|, but the minimum subset required by the web part.

\index{systems!closure}

The \textbf{closure} approach changes the route tree. It is no longer static but comes from a builder function. The function takes the components that the handlers need. Internally, we call them with a request and dependencies. This changes the function's signature: now the handler can have several arguments (request, base, cache).

To simplify the closure, a grouping component is introduced into the system. It does nothing on startup and shutdown but only accumulates dependencies. The server depends on the grouping component. Based on it, the server builds a route tree, where each handler takes a component as its first argument.

Let's introduce the grouping \verb|:web| component. So far, it only depends on the database, but in the future, it will require something else:

\begin{english}
  \begin{clojure}
(defrecord Web [db])

(defn make-web []
  (-> (map->Web {})
      (component/using [:db])))
  \end{clojure}
\end{english}

\noindent
Let's include it to the system:

\begin{english}
  \begin{clojure}
(component/system-map
 :web    (make-web)
 :server (make-server jetty)
 :db     (make-db pool)
 :worker (make-worker worker))
  \end{clojure}
\end{english}

\index{components!grouping}

Let's fix the server dependency: now it's not a database, but the grouping \verb|web| component:

\begin{english}
  \begin{clojure}
(defrecord Server
  [options server web])

(defn make-server [options]
  (-> (map->Server {:options options})
      (component/using [:web])))
  \end{clojure}
\end{english}

\index{macros!defroutes}
\index{libraries!Compojure}

Let's remember how we built routes. The \verb|defroutes| macro returns a function that accepts a request and returns a response \page{compojure}.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defroutes app
  (GET "/"
    request (page-index request))
  (GET "/hello"
    request (page-hello request))
  page-404)
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defroutes app
  (GET "/"      request (page-index request))
  (GET "/hello" request (page-hello request))
  page-404)
  \end{clojure}
\end{english}

\fi

The new route tree is not static because it depends on the component. The \verb|make-routes| function takes a grouping component and returns the routes that are enclosed within it. The \verb|page-index| function and others get two arguments: a component and a request. The component will be in the same state as it was passed in \verb|make-routes|:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn make-routes [web]
  (routes
    (GET "/"
      request (page-index web request))
    (GET "/hello"
      request (page-hello web request))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn make-routes [web]
  (routes
   (GET "/"      request (page-index web request))
   (GET "/hello" request (page-hello web request))))
  \end{clojure}
\end{english}

\fi

\noindent
The \verb|start| method of the server builds routes and passes them to \verb|run-jetty|:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(start [this]
  (let [routes (make-routes web)
        server (run-jetty
                 routes options)]
    (assoc this :server server)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(start [this]
  (let [routes (make-routes web)
        server (run-jetty routes options)]
    (assoc this :server server)))
  \end{clojure}
\end{english}

\fi

Let's take a look at the \verb|page-index| handler that accesses the database. Its first argument is \verb|web|, a component store. Let's unpack it at the signature level. Next, we turn to the \verb|db| component.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn page-index
  [{:keys [db]} request]
  (let [data (db/query db
               "select * from requests")]
    {:status 200
     :body (with-out-str
             (clojure.pprint/pprint
               data))}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn page-index
  [{:keys [db]} request]
  (let [data (db/query db "select * from requests")]
    {:status 200
     :body (with-out-str
             (clojure.pprint/pprint data))}))
  \end{clojure}
\end{english}

\fi

Forwarding and closure solve the same problem. The difference is in passing arguments to a function. Forwarding is convenient because the handler usually takes one argument, and you don't have to change the signature.

On the other hand, passing components in a request isn't always obvious. When a request has many fields, it can be difficult to work with it in testing and development. When printing a request or in logs, you will get too much output. The variant with closure and two arguments is clearer. The signature directly tells what data is expected at the input. The choice of method depends on team conventions.

\subsection{Idempotence}

Until now, we have written components so that re-running them would lead to an error. Let's demonstrate this with the example of a web server:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def s-started
  (-> {:port 8088 :join? false}
      make-server
      component/start))

(component/start s-started)
;; BindException: Address already in use
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def s-started (-> {:port 8088 :join? false}
                    make-server
                    component/start))

(component/start s-started)
;; Execution error (BindException)
;; Address already in use
  \end{clojure}
\end{english}

\fi

In the \verb|start| body, we don't check if the server is already running. If we try to start it again, we'll get an error: the port is busy. That's right: we wouldn't want two servers running. There may not be exceptions for other components. If we restart the database, we'll get a new connection pool. The old one will remain in memory and will work. As a result, resources are wasted.

\index{idempotence}

\emph{Idempotence} is property when a repeated operation returns the same result. A component has to follow it to avoid resource leaks. To do this, we check the slot before opening the resource. If the slot is \verb|nil|, we create a new server and write to the slot. Otherwise, the server is already running and \verb|this| is returned.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(start [this]
  (if server
    this
    (let [server (run-jetty
                   app options)]
      (assoc this :server server))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(start [this]
  (if server
    this
    (let [server (run-jetty app options)]
      (assoc this :server server))))
  \end{clojure}
\end{english}

\fi

\noindent
\verb|Stop| works similarly: before stopping the resource, the slot is checked for emptiness:

\begin{english}
  \begin{clojure}
(stop [this]
  (when server
    (.stop server))
  (assoc this :server nil))
  \end{clojure}
\end{english}

The variant with the \verb|or| macros is slightly shorter. We always write the slot, but the value is either the current server or the new one.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(start [this]
  (let [server (or server
                   (run-jetty
                     app options))]
    (assoc this :server server)))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(start [this]
  (let [server (or server
                   (run-jetty app options))]
    (assoc this :server server)))
  \end{clojure}
\end{english}

\fi

\subsection{Alternative Way}

Throughout this chapter, we have described components with records (typed maps). The record creates a separate class, which is extended by a protocol. When calling a method from the protocol, Clojure looks for an implementation by class.

\index{clojure.core!defrecord}
\index{protocols}
\index{metadata}

\verb|Defrecord| is the recommended way to work with Component. Of course, it has its drawback: the code with records takes up more space than functions. The record does not work with \verb|dissoc|: if you take away a slot from it, you get a regular map with an empty implementation of \verb|start| and \verb|stop|. There is a second way to describe a component. It takes less code but requires a deep understanding of how methods in Clojure work.

In fact, the thesis that method lookup works by class is incomplete. If the \verb|:extend-via-metadata| property is set for the protocol, then Clojure looks for the implementation in the object's metadata. 

Suppose you have declared an \verb|API| protocol in the \verb|project.api| namespace with this property and the \verb|get-user| method. The method expects the first argument to be an object that implements this protocol. However, it is acceptable to pass another one instead, which has the \verb|project.api/get-user| symbol with a function in its metadata. In this case, the method is resolved to the function from the metadata.

The \verb|Lifecycle| protocol has the required flag set to true. The idea is to generate an object that has implementations of Component methods in its metadata. A map is suitable for the role of the object because the library adds dependencies through \verb|assoc|.

Let's describe the server component in a new way. It consists of three functions: a constructor, \verb|start|, and \verb|stop|. The constructor takes server parameters and wraps them in a map. The \verb|with-meta| function adds metadata to the component. Metadata keys are fully qualified symbols that point at the \verb|Lifecycle| protocol methods. The keys will contain so far unknown start and stop functions:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn init [options]
  (with-meta {:options options}
    {'com.stuartsierra.component/start
     start
     'com.stuartsierra.component/stop
     stop}))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn init [options]
  (with-meta {:options options}
    {'com.stuartsierra.component/start start
     'com.stuartsierra.component/stop stop}))
  \end{clojure}
\end{english}

\fi

Let's write the starting of the component. The \verb|start| function takes the map we returned to \verb|init|. For convenience, let's call it \verb|this| and unpack the options at the signature level. Let's start the server and add it to the current map.

\begin{english}
  \begin{clojure}
(defn start
  [{:as this :keys [options]}]
  (let [server (run-jetty app options)]
    (assoc this :server server)))
  \end{clojure}
\end{english}

The stopping is similar. Note that at the end, we detach the key with \verb|dissoc|. The loss of the key does not affect anything, because the method search works by metadata rather than class. \verb|Dissoc| doesn't change the metadata.

\begin{english}
  \begin{clojure}
(defn stop
  [{:as this :keys [server]}]
  (.stop server)
  (dissoc this :server))
  \end{clojure}
\end{english}

In the system, the component behaves as usual. When the system is built, the \verb|init| function is called with configuration parameters. When starting, the library calls the \verb|start| method on all entities, and the implementation from metadata will run for the server.

Nothing changes in the dependencies: they are also specified through \verb|using| and with a vector or dictionary. The second method also works, when the constructor "charges" the component with dependencies upon creation.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn init [options]
  (->
    {:options options}
    (with-meta
     {'com.stuartsierra.component/start
      start
      'com.stuartsierra.component/stop
      stop})
    (component/using [:compA :compB])))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn init [options]
  (-> {:options options}
      (with-meta
        {'com.stuartsierra.component/start start
         'com.stuartsierra.component/stop stop})
      (component/using [:compA :compB])))
  \end{clojure}
\end{english}

\fi

\noindent
The \verb|start| and \verb|stop| methods only need to unpack them at the signature level.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn start
  [{:as this
    :keys [compA compB options]}]
  ...)
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn start
  [{:as this :keys [compA compB options]}]
  ...)
  \end{clojure}
\end{english}

\fi

Let's examine the metadata of the component after \verb|init| to make sure it has everything we need:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(-> (init {:port 8080})
    meta
    clojure.pprint/pprint)

#:com.stuartsierra.component
 {start #function[.../start]
  stop  #function[.../stop]
  :dependencies
  {:compA :compA :compB :compB}}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(-> (init {:port 8080})
    meta
    clojure.pprint/pprint)

#:com.stuartsierra.component
  {start #function[book.systems.comp.server/start]
   stop #function[book.systems.comp.server/stop]
   :dependencies {:compA :compA :compB :compB}}
  \end{clojure}
\end{english}

\fi

\index{metadata}
\index{printing!metadata}
\index{clojure.core!*print-meta*}

\noindent
For debugging such components, the \verb|*print-meta*| variable, which we talked about in the last chapter \page{print-meta}, is useful.

The alternative method does not override what we said earlier in this chapter. As with \verb|defrecord|, the component must follow the practices we've covered. Even if you like the new recording more, do not rush to switch to it. First, understand the classic components as the library recommends.

This concludes the Component overview and moves on to the third library~--- Integrant.

\section{Integrant}

\index{libraries!Integrant}

The \footurl{Integrant}{https://github.com/weavejester/integrant}[Integrant][1mm] library is the next step in the development of the system. It is at the end of the overview for several reasons. Integrant builds on the Component ideas we just discussed. The former is more flexible and generally more advanced. Let's list the main differences.

\index{OOP}

Component entities resemble classes and OOP. In Clojure, in the background of data and functions, this looks like a complication. Let a component be a function. It is simpler than an object because only one operation acts on functions~--- a call.

The component has only two states~--- \verb|start| and \verb|stop|. Integrant offers additional stages: pause and resume, spec validation, and parameter preparation. These stages are empty by default, but the component can complement them. With this approach, we have more control over the system.

\index{declarativeness}

Integrant is declarative: you can describe the system in a file and consider it as one function. That compares favorably with the Component library, where you build a system manually.

Integrant is tolerant of dependencies. In Component, a dependency requires two steps: adding a slot and metadata, but in Integrant~--- only one. In Component, a dependency can be only another component. Sometimes an object is wrapped in a component just to fulfill this requirement. In Integrant, a dependency can be anything: a dictionary, a future, or a function.

\subsection{Basic Arrangement}

Work with Integrant begins with a description of the future system. It is a map on which further logic is based. The map key is the machine name of the component, and the value is the startup parameters. The system below consists of a web server and a database.

\begin{english}
  \begin{clojure}
(def config
  {::server {:port 8080 :join? false}
   ::db {:username      "book"
         :password      "book"
         :database-name "book"
         :server-name   "127.0.0.1"
         :port-number   5432}})
  \end{clojure}
\end{english}

\index{multimethods!in Integrant}

The system and components are related through multimethods. To add a reaction to an event, we extend the required multimethod with a component key. For example, at startup, the system calls the \verb|init-key| method for each key. To explain to the system how to start the server, the method is needed to extend with the \verb|::server| key.

Integrant expects at least two methods from the key: start and stop. Since these are the main actions, there is no default response for them. Other events are optional and are up to you.

\subsection{First components}

\index{components!in Integrant}

Let's write server and database components. They are simple and have no dependencies. Prepare the \verb|integrant.clj| module with a header:

\begin{english}
  \begin{clojure}
(ns book.integrant
  (:require [integrant.core :as ig]))
  \end{clojure}
\end{english}

For the sake of brevity, let's skip the imports of \verb|Jetty|, \verb|HikariCP|, and other libraries. They are similar to the examples from Mount and Component.

Let's begin with the server. The \verb|init-key| method takes a key and an options map. These are the \verb|::server| and \verb|{:port 8080: join? false}| values from config. This method should return the state of the component. In our case, this is the result of \verb|run-jetty|.

\begin{english}
  \begin{clojure}
(defmethod ig/init-key ::server
  [_ options]
  (run-jetty app options))
  \end{clojure}
\end{english}

\index{connection pool}

The key is known from the method definition, so the first argument is shadowed with an underscore. Let's describe the database in a similar way. Its state is a JDBC spec with a connection pool.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defmethod ig/init-key ::db
  [_ options]
  {:datasource
    (cp/make-datasource options)})
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defmethod ig/init-key ::db
  [_ options]
  {:datasource (cp/make-datasource options)})
  \end{clojure}
\end{english}

\fi

The \verb|init| function runs through the configuration and calls \verb|init-key| for each key. We should get a dictionary with the same keys, but the component state will appear in place of the parameters. Let's save the running system to a variable to turn it off later.

\index{initialization}

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def _sys (ig/init config))

(keys _sys)
(:book.integrant/db
 :book.integrant/server)
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def _sys (ig/init config))

(keys _sys)
(:book.integrant/db :book.integrant/server)
  \end{clojure}
\end{english}

\fi

The shutdown of the system is called a \verb|halt|. The \verb|halt-key!| method determines how to shut down the component. It takes the key and state from the \verb|init-key| method. Let's describe the halt of the server and database:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defmethod ig/halt-key! ::server
  [_ server]
  (.stop server))

(defmethod ig/halt-key! ::db
  [_ db-spec]
  (-> db-spec
      :datasource
      cp/close-datasource))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defmethod ig/halt-key! ::server
  [_ server]
  (.stop server))

(defmethod ig/halt-key! ::db
  [_ db-spec]
  (-> db-spec :datasource cp/close-datasource))
  \end{clojure}
\end{english}

\fi

\noindent
The \verb|halt!| function will halt the entire system. Pass the result of \verb|ig/init| to it.

\begin{english}
  \begin{clojure}
(ig/halt! _sys)
  \end{clojure}
\end{english}

\subsection{Dependencies}

\index{dependency graph}
\index{dependencies!Integrant}

To specify dependencies, a reference is added to the options. At startup, Integrant searches for references in the system and builds a dependency graph. Set the reference with the \verb|ig/ref| function. It takes a key that the component depends on.

Let's consider the dependency on the example of a worker. Add a key to the config, as in the example below. To separate component options from dependencies, put them in the separate \verb|:options| field.

\begin{english}
  \begin{clojure}
{::worker {:options {:sleep 1000}
           :db (ig/ref ::db)}}
  \end{clojure}
\end{english}

When the \verb|init-key| reaches the \verb|::worker| key, \verb|:db| will contain the value that \verb|init-key| returned for \verb|::db|. Here is the code for starting and stopping the worker. Since this is already the third implementation, we will leave only the main part. If you need to brush up on how a worker works, refer to the section where we wrote it for the first time \page{worker}.

\begin{english}
  \begin{clojure}
(defmethod ig/init-key ::worker
  [_ {:keys [db options]}]
  (let [flag (atom true)
        task (make-task db flag options)]
    {:flag flag :task task}))

(defmethod ig/halt-key! ::worker
  [_ {:keys [flag task]}]
  (reset! flag false)
  (while (not (realized? task))
    (Thread/sleep 300)))
  \end{clojure}
\end{english}

\subsection{Parallels with Component}

Several techniques we covered in the Component section also work in Integrant. Let's recall some of them.

\index{systems!storage}

\textbf{Global storage}: To manage the system, you need to store it somewhere. The easiest way to do this is to add a global variable and start and stop functions.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defonce ^:private system nil)

(def alter-system
  (partial alter-var-root #'system))

(defn system-start []
  (alter-system
    (constantly (ig/init config))))

(defn system-stop []
  (alter-system ig/halt!))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defonce ^:private system nil)

(def alter-system (partial alter-var-root #'system))

(defn system-start []
  (alter-system (constantly (ig/init config))))

(defn system-stop []
  (alter-system ig/halt!))
  \end{clojure}
\end{english}

\fi

As with Component, the system must be private. Free access to it is unacceptable for consumers.

\index{systems!waiting}

\textbf{Waiting and signals}: An application waits for all components to stop before exiting. The \verb|with-handler| macro and signal interception work in the same way for Integrant:

\begin{english}
  \begin{clojure}
(with-handler :term
  (log/info "caught SIGTERM, quitting")
  (system-stop)
  (log/info "all components shut down")
  (exit))
  \end{clojure}
\end{english}

\index{systems!descent}

\textbf{Descent system and routes}: In Integrant, it is easier to access the system from an HTTP request. The handler can be a database-dependent component. Let the page display the number of records in the database. Let's add a new key and a reference to the database to the system:

\begin{english}
  \begin{clojure}
{::handler {:db (ig/ref ::db)}}
  \end{clojure}
\end{english}

\noindent
In the run of the key, we return the request handler closed in \verb|db|:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defmethod ig/init-key ::handler
  [_ {:keys [db]}]
  (fn [request]
    (let [query "select count(*) as total
                 from requests"
          result (jdbc/query db query)
          total (-> result first :total)]
      {:status 200
       :body (format
               "You've got %s records."
               total)})))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defmethod ig/init-key ::handler
  [_ {:keys [db]}]
  (fn [request]
    (let [query "select count(*) as total from requests"
          result (jdbc/query db query)
          total (-> result first :total)]
      {:status 200
       :body (format "You've got %s records." total)})))
  \end{clojure}
\end{english}

\fi

\noindent
Let's modify the server so that it depends on the handler:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
{::server {:options {:port 8080
                     :join? false}
           :handler (ig/ref ::handler)}}

(defmethod ig/init-key ::server
  [_ {:keys [handler options]}]
  (run-jetty handler options))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
{::server {:options {:port 8080 :join? false}
           :handler (ig/ref ::handler)}}

(defmethod ig/init-key ::server
  [_ {:keys [handler options]}]
  (run-jetty handler options))
  \end{clojure}
\end{english}

\fi

The browser will show the phrase "You've got N records," where \verb|N| is the number of the database records. As with Clojure, \verb|::handler| can return both a single page and a route tree, using Compojure.

\index{systems!conditional}

\textbf{Conditional build}: The system can be changed conditionally before starting. A special function determines if a worker can be run on this machine. If yes, add the component key and its settings to the system.

\begin{english}
  \begin{clojure}
(cond-> sys-config
  (is-worker-supported?)
  (assoc ::worker {:options {:sleep 1000}
                   :db (ig/ref ::db)}))
  \end{clojure}
\end{english}

Another way to start a subset of the system is similar to Mount \page{mount-selective}. The \verb|init| function accepts an optional list of keys to include. The list is prepared in advance according to a rule. Below we take all the configuration keys and remove some by the condition.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(let [components (-> config keys set)
      components
      (cond-> components
        (not (is-worker-supported?))
        (disj ::worker))]
  (ig/init config components))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(let [components (-> config keys set)
      components (cond-> components
                   (not (is-worker-supported?))
                   (disj ::worker))]
  (ig/init config components))
  \end{clojure}
\end{english}

\fi

\subsection{Loss of Keys}

\index{keys!losses}
\index{syntax!:: (namespace)}

Specify for components fully qualified keys (e.g., \verb|::server|, \verb|::db|). The double colon stands for the current namespace in which the key is declared. The \verb|::db| notation is a shorthand for \verb|:book.integrant/db|.

When a key is fully qualified (with a namespace), it is easy to understand in which module it is declared. There are more than ten components in systems. Let's say there is a problem with the \verb|:queue| key. How to understand which module it is in? Whereas the \texttt{:my-project.utils.queue\-/queue} key contains this information. I recommend you always use fully qualified keys.

A situation may arise that you forgot to import the module in which you extended the multimethod. If the module is not loaded, Integrant will not know about the component. Sometimes you are left completely confused: you remember that you wrote the code; however, there is no component. We've discussed a similar problem in Mount \page{mount-main}. To avoid an error, add all modules with components to the header of the main module, which is always loaded. Let it be the system module.

\begin{english}
  \begin{clojure}
(ns project.system
  (:require project.db
            project.server
            project.worker
            project.utils.queue))
  \end{clojure}
\end{english}

Syntax checking utilities (linters) may generate a warning. In their terms, you've added a module, but you are not using it because there is no \verb|project.db/<something>| expression in your code. To suppress such warnings, correct linter settings. Add \verb|project.db| into the known namespaces section or similar.

\index{namespaces}

\mnoindent
Integrant offers the \verb|load-namespaces| function to load modules automatically. The system configuration is fed to its input. For each key, the function looks for its space and loads it. A high-performance system with keys from different modules looks like this:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def config
  {:project.server/server
   {:options {:port 8080 :join? false}
    :handler (ig/ref
               :project.handlers/index)}
   :project.db/db {...}
   :project.worker/worker
   {:options {:sleep 1000}
    :db      (ig/ref :project.db/db)}
   :project.handlers/index
   {:db (ig/ref :project.db/db)}})
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def config
  {:project.server/server
   {:options {:port 8080 :join? false}
    :handler (ig/ref :project.handlers/index)}
   :project.db/db {...}
   :project.worker/worker
   {:options {:sleep 1000}
    :db      (ig/ref :project.db/db)}
   :project.handlers/index
   {:db (ig/ref :project.db/db)}})
  \end{clojure}
\end{english}

\fi

\noindent
To load all modules that participate in the system, execute the following:

\begin{english}
  \begin{clojure}
(ig/load-namespaces config)
  \end{clojure}
\end{english}

We recommend beginners to refrain from automatic imports. Add them explicitly to \verb|ns|: the method, although verbose, is obvious. Use \verb|load-namespaces| only if you fully understand how namespaces and their loading work.

\subsection{System in File}

\index{declarativeness}

Integrant encourages a declarative approach. System configuration is a static dictionary. For space-saving, the system is taken to the EDN resource and read at the start of the application.

\index{tags!Integrant}

You might notice that we have specified references with the \verb|ig/ref| function, and it is not entirely clear how to express them in the file. The \verb|#ig/ref| tag works like the function of the same name.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
{:project.worker/worker
 {:options {:sleep 1000}
  :db #ig/ref :project.db/db}}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
{:project.worker/worker {:options {:sleep 1000}
                         :db #ig/ref :project.db/db}}
  \end{clojure}
\end{english}

\fi

\index{modules!clojure.edn}

\mnoindent
Integrant offers its \verb|read-string| version for a reading of EDN. It is a wrapper over the usual \verb|clojure.edn/read-string|, but with tags from the \verb|#ig/| family. To read the system from a file, do the following:

\begin{english}
  \begin{clojure}
(def config
  (-> "config.edn" slurp ig/read-string))
  \end{clojure}
\end{english}

\index{variables!environment}

From the configuration chapter, we remember that it is undesirable to store passwords and access keys in a file \page{password-note}. The \verb|project.db/db| component violates this principle: the password for the database is written publicly. Let's make the parser read the password from the environment.

We'll put the configuration into the \texttt{integrant\-.test.edn} file (snippet):

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
{:project.db/db
 {:password #env DB_PASSWORD}
 :project.worker/worker
 {:options {:sleep 1000}
  :db #ig/ref :project.db/db}}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
{:project.db/db {:password #env DB_PASSWORD}
 :project.worker/worker {:options {:sleep 1000}
                         :db #ig/ref :project.db/db}}
  \end{clojure}
\end{english}

\fi

The \verb|ig/read-string| function takes additional EDN tags. Integrant will combine them with its ones when reading the file. Let's wrap the read configuration in the function. The first argument of \verb|ig/read-string| is the map with the \verb|#env| tag from the previous chapter. Integrant supplements our map with its own one, so both \verb|#ig/ref| and \verb|#env| will work.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(defn load-config [filename]
  (ig/read-string
    {:readers {'env tag-env}}
    (slurp filename)))

(load-config "integrant.test.edn")
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(defn load-config [filename]
  (ig/read-string {:readers {'env tag-env}}
                  (slurp filename)))

(load-config "integrant.test.edn")
  \end{clojure}
\end{english}

\fi

\noindent
Here is the result:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
{:project.db/db
 {:password "c8497b517da25"}
 :project.worker/worker
 {:options {:sleep 1000}
  :db
  #integrant.core.Ref{:key
                      :project.db/db}}}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
{:project.db/db {:password "c8497b517da25"}
 :project.worker/worker
 {:options {:sleep 1000}
  :db #integrant.core.Ref{:key :project.db/db}}}
  \end{clojure}
\end{english}

\fi

\subsection{Key Inheritance}

\index{keys!inheritance}
\index{inheritance}

In Clojure, keys can be hierarchical. The \verb|derive| function takes two keys and sets the superiority of the first over the second.

\index{clojure.core!derive}

\begin{english}
  \begin{clojure}
(derive ::postgresql ::database)
  \end{clojure}
\end{english}

\mnoindent
When the multimethod looks for an action by a key, it takes inheritance into account. If the method is specified for \verb|::database|, calling from \verb|::postgresql| will not result in an error: the \verb|::database| version will work.

Integrant runs on multimethods, so inheritance is useful. Suppose we have a project with two databases: a master for writing and a replica for reading. The \verb|::db-master| and \verb|::db-replica| components differ only in their input parameters.

If we didn't know about inheritance, we would write \verb|ig/init-key| and \verb|ig/halt-key!| for each key. It is considered bad practice to repeat code. We have described the \verb|::db| component for database sharing. Let's inherit two new ones from it:

\begin{english}
  \begin{clojure}
(derive ::db-master ::db)
(derive ::db-replica ::db)
  \end{clojure}
\end{english}

Let's update the configuration. For the replica, set the \verb|:read-only| flag to protect ourselves from writing to the wrong source. Pay attention to the dependencies. The worker \emph{writes} data to the database and therefore refers to \verb|::db-master|. The \verb|::hander| component only \emph{reads} data, so it depends on \verb|::db-replica|. See Listing \ref{fig:config-refset} for a config example.

\begin{listing}[ht!]

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def config
  {::server {:options {:port 8080
                       :join? false}
             :handler (ig/ref ::handler)}
   ::db-master {;; other fields
                :read-only false}
   ::db-replica {;; other fields
                 :read-only true}
   ::worker {:options {:sleep 1000}
             :db (ig/ref ::db-master)}
   ::handler {:db (ig/ref
                    ::db-replica)}})
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def config
  {::server {:options {:port 8080 :join? false}
             :handler (ig/ref ::handler)}
   ::db-master {;; other fields
                :read-only false}
   ::db-replica {;; other fields
                 :read-only true}
   ::worker {:options {:sleep 1000}
             :db (ig/ref ::db-master)}
   ::handler {:db (ig/ref ::db-replica)}})
  \end{clojure}
\end{english}

\fi

\caption{Configuration with two databases and references to them}
\label{fig:config-refset}

\end{listing}

The function and the \verb|ig/refset| tag will return many references based on the hierarchy. Let one of the components is waiting for all databases to make a service request to them. In order not to refer to each database manually, specify the root key \verb|::db|.

To do this, add the \texttt{::db-main\-te\-nance} component. It depends on all databases through \verb|refset|. The \verb|init-key| method will return a background task that runs through the databases and executes the request. Its starting and stopping are similar to the worker. The example below shows how to get to both databases from the parameters.

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
{::db-maintenance
 {:dbs (ig/refset ::db)}}

(defmethod ig/init-key ::db-maintenance
  [_ {:keys [dbs]}]
  (future
    (every-1h-interval
      (doseq [db dbs]
        (run-system-query db)))))
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
{::db-maintenance {:dbs (ig/refset ::db)}}

(defmethod ig/init-key ::db-maintenance
  [_ {:keys [dbs]}]
  (future
    (every-1h-interval
      (doseq [db dbs]
        (run-system-query db)))))
  \end{clojure}
\end{english}

\fi

\subsection{Other Component Stages}

\index{components!stages}

In addition to starting and stopping, Integrant offers other stages of a component. Unlike \verb|init| and \verb|halt!|, their multimethods will return \verb|nil| or the original object by default (i.e., an empty action). To subscribe a component to an event, extend the multimethod with its key. Let's take a look at a few useful stages.

\subsection{Preparation}

The \verb|ig/prep-key| method prepares parameters. It basically combines them with the default set. Suppose, through trial and error, we have found the ideal database pool metrics. To avoid specifying all fields in the configuration, we will move them to the default map.

\begin{english}
  \begin{clojure}
(def db-defaults
  {:auto-commit        false
   :read-only          false
   :connection-timeout 30000
   :idle-timeout       600000
   :max-lifetime       1800000
   :maximum-pool-size  10})

(defmethod ig/prep-key ::db
  [_ options]
  (merge db-defaults options))
  \end{clojure}
\end{english}

The \verb|prep-key| method combines it with parameters. In the config, it is enough to specify only the access fields and, if required, overrides for special cases:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
{::db {;; override the default
       :auto-commit   true
       :adapter       "postgresql"
       :username      "book"
       :password      "book"
       :database-name "book"
       :server-name   "127.0.0.1"}}
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
{::db {:auto-commit   true ;; override the default
       :adapter       "postgresql"
       :username      "book"
       :password      "book"
       :database-name "book"
       :server-name   "127.0.0.1"}}
  \end{clojure}
\end{english}

\fi

The \verb|ig/prep| function takes the configuration and prepares each key. To remember this step, add it to \verb|load-config|.

\subsection{Spec}

\index{spec!in Integrant}

The \verb|ig/pre-init-spec| method binds a component to a spec. If the method returns a spec, it will check the parameters of the component. Make sure we set all the fields in the database connection:

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(require '[clojure.spec.alpha :as s])

(s/def :db/username string?)
(s/def :db/database-name string?)
;; host, password, etc

(defmethod ig/pre-init-spec ::db [_]
  (s/keys :req-un [:db/username
                   :db/database-name]))
                   ;; etc
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(require '[clojure.spec.alpha :as s])

(s/def :db/username string?)
(s/def :db/database-name string?)
;; host, password, etc

(defmethod ig/pre-init-spec ::db [_]
  (s/keys :req-un [:db/username
                   :db/database-name])) ;; etc
  \end{clojure}
\end{english}

\fi

\noindent
If we pass invalid parameters, we'll get a Spec error. Its message and \verb|explain| report are familiar to you from previous chapters \page{spec-explain}.

\subsection{Suspending}

\index{systems!stopping}

In addition to starting and stopping, Integrant offers the third state of the system~--- \verb|suspended|. In it, the component does not lose its state but only pauses internal processes. If this component is a consumer of messages from a queue, it stops reading the source but does not close the connection. The inverse operation to suspending is \verb|resume|. When resuming, the component continues to work without creating new connections.

\mnoindent
By default, these events work like \verb|halt| and \verb|init|. Extend the \texttt{ig/sus\-pend\--key!} and \verb|ig/resume-key| methods to specify a custom response to \verb|suspend| and \verb|resume|. This will require an in-depth reading of the documentation. Good luck with your independent study on the topic!

\section{Summary}

Just as a machine is made up of parts, a program consists of components. They are governed by a system~--- an agreement on how components are arranged and related to each other.

Every project needs a system, and the longer it develops, the stronger the need. If there is no agreement in the project on how to write the constituent parts, it starts to slow down. Over time, the project will become too expensive to maintain.

Clojure offers several ways to build a system. The most popular libraries are Mount, Component, and Integrant. They have different approaches so that every developer will find what he likes.

Mount relies on global variables. If the project is written as in the example 
below

\ifx\DEVICETYPE\MOBILE

\begin{english}
  \begin{clojure}
(def server
  (run-jetty app {:port 8080})),
  \end{clojure}
\end{english}

\else

\begin{english}
  \begin{clojure}
(def server (run-jetty app {:port 8080})),
  \end{clojure}
\end{english}

\fi

\noindent
then porting it to Mount will be easy.
The \verb|server| variable will become an entity that changes its value on command. Mount is fine for those new to Clojure.

Component takes a step towards real components. We define entities that isolate the state. Access to a component is determined by the methods that it implements. Components and protocols are similar to objects from modern languages. For this reason, some people accuse the Component of being bloated and too enterprise-like.

Sometimes the solution using components takes up more space than with atoms and functions. On the other hand, it is Component that gives an understanding of how to build sustainable systems. Note, that we discussed most of the questions in the section about Component.

The Integrant project goes further: it is free of the OOP cumbersomeness and is generally more Clojure-friendly. Integrant draws on Clojure idioms and techniques to win over experienced developers.

Our goal is not to find out which library is better. Don't rush to rewrite your project from conditional Mount to Component or vice versa. This is exhausting work, and you will not understand what benefits you have achieved until you feel the need for them.

Instead of arguing about \emph{which} system is better, think about \emph{why} the project needs it. When the answer is clear, technical solutions will come up by themselves.
